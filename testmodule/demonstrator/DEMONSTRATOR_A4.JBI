/JOB
//NAME DEMONSTRATOR_A4
///FOLDERNAME DEMONSTRATOR
//POS
///NPOS 0,0,0,1,0,0
///TOOL 1
///POSTYPE ROBOT
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=99000.000,99000.000,99000.000,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,1,0,0,0,0,0
D003 H4_GESAMT_IST
///LVARS 0,2,27,0,0,0,0,0
LI000 Point
LI001 Direction
LD000 HALBKREIS
LD001 X_right
LD002 Y_upper
LD003 MX
LD004 MY
LD005 RADIUS
LD006 Y_lower
LD007 X_offset
LD008 X
LD009 X_left
LD010 Z_left
LD011 Z_right
LD012 USERFRAME
LD013 Z_offset
LD014 Width
LD015 Beads_inner
LD016 Z
LD018 V_weld
LD019 KENNLINIE
LD020 V_weld_offset
LD021 X_left_inner
LD022 X_right_inner
LD023 Beads_outer
LD024 Spurbreite
LD025 Uebermass
LD026 X_height
LD027 Y_height
//ARGINFO
///ARGTYPE D,D,D,D,D,D,D,D
///COMMENT
X left
X right
X Offset
Height_left
Height_right
USERFRAME
V_WELD
Kennlinie
//INST
///DATE 2023/01/24 14:22
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,3,28,0,0,0,0,0
NOP
TCPON TL#(0)
CALL JOB:SET_TCPON ARGF0
'################################
'Einlesen der Argumente
GETARG X_left IARG#(1)
GETARG X_right IARG#(2)
GETARG X_offset IARG#(3)
GETARG Z_left IARG#(4)
GETARG Z_right IARG#(5)
GETARG USERFRAME IARG#(6)
GETARG V_weld IARG#(7)
GETARG KENNLINIE IARG#(8)
'################################
SET V_weld_offset  EXPRESS V_weld * 2
'################################
SET Z Z_left
SET Point 0
SET Direction 1
SET Spurbreite 9000
SET Uebermass 4000
'################################
'Berechnen der fertigbaren Breite
' Width = vielfaches von x_Offset
' Gerade Anzahl von Raupen
SET Beads_outer  EXPRESS ( X_right - X_left ) / ( X_offset )
CALL JOB:GET_EVEN_NUMBER ARGFBeads_outer ARGF"DOWN"
GETS Beads_outer $RV
ADD Beads_outer 1
SET Width  EXPRESS X_offset * Beads_outer
'################################
'################################
'Berechnen der inneren Grenezen
SET X_left_inner  EXPRESS Width * -0.5
SET X_right_inner X_left_inner
WHILEEXP X_right_inner<-51070
	 ADD X_right_inner X_offset
ENDWHILE
SET X_right_inner  EXPRESS X_right_inner - X_offset
'Berechnung der Anzahl der
'Punkte in der Teilflaeche
'Fehlermeldung if P>98 -->
'98 globale Punktvariablen
SET Beads_inner  EXPRESS ( X_right_inner - X_left_inner ) / ( X_offset )
SET Point  EXPRESS Beads_inner * 2
IFTHENEXP Point>98
	 SET B000  EXPRESS 400
	 MSG "STRING"
ENDIF
'################################
'Kontrolle
SET D030 Width
SET D031 X_left_inner
SET D032 X_right_inner
SET D033 Beads_outer
SET D034 Beads_inner
SET D035  EXPRESS X_right_inner - X_left_inner
SET D036  EXPRESS D035 / D034
'###############################
SET Z_offset  EXPRESS ( Z_right - Z_left ) / ( Beads_inner )
SET X X_left_inner
SET Point 0
WHILEEXP X<=X_right_inner
	'Obere Kontur
	'Kreis eins
	 IFTHENEXP X<=-70000
		 SET MX 0
		 SET MY -640000
		 SET RADIUS 625000
		 SET HALBKREIS 1
		 CALL JOB:GET_KREISKOORDINATEN ARGFX ARGFMX ARGFMY ARGFRADIUS ARGFHALBKREIS
	 ELSEIFEXP X>-70000 ANDEXP X<-51070
		 SET MX -70000
		 SET MY 0
		 SET RADIUS 18930
		 SET HALBKREIS -1
		 CALL JOB:GET_KREISKOORDINATEN ARGFX ARGFMX ARGFMY ARGFRADIUS ARGFHALBKREIS
	 ENDIF
	 GETS Y_upper $RV
	 SET Y_upper  EXPRESS Y_upper - 0.5 * Spurbreite + Uebermass
	'untere Kontur (Kreis)
	 IFTHENEXP X<-51070
		 SET MX 0
		 SET MY -640000
		 SET RADIUS 600000
		 SET HALBKREIS 1
		 CALL JOB:GET_KREISKOORDINATEN ARGFX ARGFMX ARGFMY ARGFRADIUS ARGFHALBKREIS
	 ENDIF
	 GETS Y_lower $RV
	 SET Y_lower  EXPRESS Y_lower + 0.5 * Spurbreite - Uebermass
	'Punkte schreiben
	 MUL Y_lower -1
	 MUL Y_upper -1
	 IFTHENEXP Point=2
		 SET X_height  EXPRESS X * 1
		 SET Y_height  EXPRESS ( Y_upper + Y_lower ) / 2
	 ENDIF
	 IFTHENEXP Direction=1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY_lower ARGFZ
		 SET P[Point] P100
		 ADD Point 1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY_upper ARGFZ
		 SET P[Point] P100
		 ADD Point 1
		 MUL Direction -1
	 ELSEIFEXP Direction=-1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY_upper ARGFZ
		 SET P[Point] P100
		 ADD Point 1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY_lower ARGFZ
		 SET P[Point] P100
		 ADD Point 1
		 MUL Direction -1
	 ENDIF
	 ADD Z Z_offset
	 ADD X X_offset
ENDWHILE
'########################
'##########Welding#######
CALL JOB:DEMONSTRATOR_WELD_MEAND ARGFPoint ARGFV_weld ARGFKENNLINIE ARGF61
'Der verschobene HoehePKT
'wird in P100 geschrieben
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_height ARGFY_height ARGFH4_GESAMT_IST
END
