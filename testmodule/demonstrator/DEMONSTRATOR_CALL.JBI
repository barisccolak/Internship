/JOB
//NAME DEMONSTRATOR_CALL
///FOLDERNAME DEMONSTRATOR
//POS
///NPOS 0,0,0,1,0,0
///TOOL 1
///POSTYPE ROBOT
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=99000.000,99000.000,99000.000,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,16,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D002 H3_GESAMT_IST
D003 H4_GESAMT_IST
D004 H5_GESAMT_IST
D005 H6_GESAMT_IST
D006 H7_GESAMT_IST
D007 H8_GESAMT_IST
D010 H1_LAGE_IST
D011 H2_LAGE_IST
D012 H3_LAGE_IST
D013 H4_LAGE_IST
D014 H5_LAGE_IST
D015 H6_LAGE_IST
D016 H7_LAGE_IST
D017 H8_LAGE_IST
///LVARS 0,1,21,0,0,8,0,0
LI000 Layer
LD000 X_left
LD001 X_right
LD002 X_offset
LD003 USERFRAME
LD004 V_weld
LD005 Z_layer
LD006 Z_start
LD007 H_soll
LD008 V_MOVE
LD009 V_SCAN
LD010 H_temp
LD011 KENNLINIE
LD012 Kenn_L1
LD013 Kenn_L2
LD014 Kenn_L3
LD015 Kenn_REST
LD016 Vers_L1
LD017 Vers_L2
LD018 Vers_L3
LD019 Vers_REST
LD020 MIN_HEIGHT
LP000 PKT_H1
LP001 PKT_H2
LP002 PKT_H3
LP003 PKT_H4
LP004 PKT_H5
LP005 PKT_H6
LP006 PKT_H7
LP007 PKT_H8
//INST
///DATE 2023/10/24 13:58
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,2,26,0,0,10,0,0
NOP
CALL JOB:TRIGGER_RESET
'ID'S SETZEN
CALL JOB:SET_IDS_FULL ARGF513 ARGF1 ARGF12 ARGF1 ARGF0
'--------------------------------
SET USERFRAME 4
SET H_soll 50000
SET Z_start 30000
'Kennlinien
SET Kenn_L1 1
SET Kenn_L2 2
SET Kenn_L3 3
SET Kenn_REST 4
'Versaetze
SET Vers_L1 4900
SET Vers_L2 4650
SET Vers_L3 4400
SET Vers_REST 4300
'--------------------------------
'WELD-Geschwindigkeit
CALL JOB:GET_V ARGF57 ARGF"cm/min"
GETS V_weld $RV
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF15 ARGF"mm/s"
GETS V_SCAN $RV
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'--------------------------------
'USERFRAME in TwinCAT setzen
CALL JOB:SET_UFRAME ARGFUSERFRAME
'Berechnungen
'--------------------------------
SET H1_GESAMT_IST Z_start
SET H2_GESAMT_IST Z_start
SET H3_GESAMT_IST Z_start
SET H4_GESAMT_IST Z_start
SET H5_GESAMT_IST Z_start
SET H6_GESAMT_IST Z_start
SET H7_GESAMT_IST Z_start
SET H8_GESAMT_IST Z_start
'--------------------------------
'Variable HoehenmessPKT erzeugen
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF-200000 ARGF-50000 ARGFH1_GESAMT_IST
SET PKT_H1 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF200000 ARGF-50000 ARGFH2_GESAMT_IST
SET PKT_H2 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF200000 ARGF50000 ARGFH3_GESAMT_IST
SET PKT_H3 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF-200000 ARGF50000 ARGFH4_GESAMT_IST
SET PKT_H4 P100
'Feste Hoehenmesspunkte erzeugen
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF-37000 ARGF-27000 ARGFH5_GESAMT_IST
SET PKT_H5 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF37000 ARGF-27000 ARGFH6_GESAMT_IST
SET PKT_H6 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF37000 ARGF27000 ARGFH7_GESAMT_IST
SET PKT_H7 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF-37000 ARGF27000 ARGFH8_GESAMT_IST
SET PKT_H8 P100
'--------------------------------
'Beginn der Fertigung
'--------------------------------
'INITIALE HOEHENMESSUNG
'Nur innerer bereich
'H5 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H5 ARGF0
GETS H5_LAGE_IST $RV
ADD H5_GESAMT_IST H5_LAGE_IST
'H6 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H6 ARGF0
GETS H6_LAGE_IST $RV
ADD H6_GESAMT_IST H6_LAGE_IST
'H7 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H7 ARGF0
GETS H7_LAGE_IST $RV
ADD H7_GESAMT_IST H7_LAGE_IST
'H8 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H8 ARGF0
GETS H8_LAGE_IST $RV
ADD H8_GESAMT_IST H8_LAGE_IST
'---------------------------
'Mittlere Bauteilhoehe berechnen
CALL JOB:MEAN_BACK_8 ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST
GETS Z_layer $RV
'Minimale Bauteilhoehe berechnen
CALL JOB:MIN_BACK_8 ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST
GETS MIN_HEIGHT $RV
'mittlere gemessene Hoehe in
' H1 - H4 schreiben
SET H1_GESAMT_IST Z_layer
SET H2_GESAMT_IST Z_layer
SET H3_GESAMT_IST Z_layer
SET H4_GESAMT_IST Z_layer
'wenn z_layer kleiner 0
' z_layer = 0 setzen
IFTHENEXP MIN_HEIGHT<0
	 SET MIN_HEIGHT 0
ENDIF
IFTHENEXP Z_layer<0
	 SET Z_layer 0
ENDIF
CALL JOB:SCAN_AREA ARGFUSERFRAME ARGF-200000 ARGF-120000 ARGFZ_layer ARGF410000 ARGF239000 ARGF1
WHILEEXP MIN_HEIGHT<=H_soll
	 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
	'Lage wird um 1 erhoeht
	 CALL JOB:GET_ID ARGF"LAGE_NR"
	 GETS Layer $RV
	 CALL JOB:CHOOSE_ARG_BY_LAYER ARGFVers_L1 ARGFVers_L2 ARGFVers_L3 ARGFVers_REST ARGFLayer
	 GETS X_offset $RV
	 CALL JOB:CHOOSE_ARG_BY_LAYER ARGFKenn_L1 ARGFKenn_L2 ARGFKenn_L3 ARGFKenn_REST ARGFLayer
	 GETS KENNLINIE $RV
	'linke u Rechte Kontur Berechnen
	 CALL JOB:GET_LEFT_CONTOUR ARGFZ_layer
	 GETS X_left $RV
	 CALL JOB:GET_RIGHT_CONTOUR ARGFZ_layer
	 GETS X_right $RV
	'Flche 1, linker unterer Arm
	 CALL JOB:DEMONSTRATOR_A1 ARGFX_left ARGFX_right ARGFX_offset ARGFH1_GESAMT_IST ARGFH5_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 SET PKT_H1 P100
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 2, rechter unterer Arm
	 CALL JOB:DEMONSTRATOR_A2 ARGFX_left ARGFX_right ARGFX_offset ARGFH2_GESAMT_IST ARGFH6_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 SET PKT_H2 P100
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 3, rechter oberer Arm
	 CALL JOB:DEMONSTRATOR_A3 ARGFX_left ARGFX_right ARGFX_offset ARGFH3_GESAMT_IST ARGFH7_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 SET PKT_H3 P100
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 4, linker oberer Arm
	 CALL JOB:DEMONSTRATOR_A4 ARGFX_left ARGFX_right ARGFX_offset ARGFH4_GESAMT_IST ARGFH8_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 SET PKT_H4 P100
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 5, links vom Loch
	 SET H_temp  EXPRESS ( H8_GESAMT_IST + H5_GESAMT_IST ) / 2
	 CALL JOB:DEMONSTRATOR_A5 ARGFX_left ARGFX_right ARGFX_offset ARGFH_temp ARGFH_temp ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 6, rechts vom Loch
	 SET H_temp  EXPRESS ( H7_GESAMT_IST + H6_GESAMT_IST ) / 2
	 CALL JOB:DEMONSTRATOR_A6 ARGFX_left ARGFX_right ARGFX_offset ARGFH_temp ARGFH_temp ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 7, ueberm Loch
	 CALL JOB:DEMONSTRATOR_A7 ARGFX_left ARGFX_right ARGFX_offset ARGFH8_GESAMT_IST ARGFH7_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Flche 8, unterm Loch
	 CALL JOB:DEMONSTRATOR_A8 ARGFX_left ARGFX_right ARGFX_offset ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFUSERFRAME ARGFV_weld ARGFKENNLINIE
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
	'Timer debug Messfehler in
	'Hoehenmessung
	 TIMER T=60.00
	'--------------------------------
	'Wiederholte HOEHENMESSUNG
	'H1 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H5 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H5 ARGF0
	 GETS H5_LAGE_IST $RV
	 ADD H5_GESAMT_IST H5_LAGE_IST
	'H6 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H6 ARGF0
	 GETS H6_LAGE_IST $RV
	 ADD H6_GESAMT_IST H6_LAGE_IST
	'H2 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'H3 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H3 ARGF0
	 GETS H3_LAGE_IST $RV
	 ADD H3_GESAMT_IST H3_LAGE_IST
	'H7 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H7 ARGF0
	 GETS H7_LAGE_IST $RV
	 ADD H7_GESAMT_IST H7_LAGE_IST
	'H8 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H8 ARGF0
	 GETS H8_LAGE_IST $RV
	 ADD H8_GESAMT_IST H8_LAGE_IST
	'H4 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFPKT_H4 ARGF0
	 GETS H4_LAGE_IST $RV
	 ADD H4_GESAMT_IST H4_LAGE_IST
	'gemessene Hoehen in Hoehen-
	'messpunkte bertragen
	 SETE PKT_H1 (3) H1_GESAMT_IST
	 SETE PKT_H2 (3) H2_GESAMT_IST
	 SETE PKT_H3 (3) H3_GESAMT_IST
	 SETE PKT_H4 (3) H4_GESAMT_IST
	 SETE PKT_H5 (3) H5_GESAMT_IST
	 SETE PKT_H6 (3) H6_GESAMT_IST
	 SETE PKT_H7 (3) H7_GESAMT_IST
	 SETE PKT_H8 (3) H8_GESAMT_IST
	'Mittlere Lagenhhe berechnen
	'Nchste Grundhoehe fuer Kontur
	 CALL JOB:MEAN_BACK_8 ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST
	 GETS Z_layer $RV
	 CALL JOB:MIN_BACK_8 ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST ARGFH5_GESAMT_IST ARGFH6_GESAMT_IST ARGFH7_GESAMT_IST ARGFH8_GESAMT_IST
	 GETS MIN_HEIGHT $RV
	 SET D035 Z_layer
	 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
	 CALL JOB:INC_NR ARGF"LAGE_NR"
ENDWHILE
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
