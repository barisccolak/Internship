/JOB
//NAME CREATE_SHIFTED_UF
///FOLDERNAME INTERNE_BERECHNUNGEN
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 1,0,11,0,0,8,0,0
LB000 ABBRUCH
LD000 X_NEW
LD001 Y_NEW
LD002 OLD_UF
LD003 NEW_UF
LD004 ROT_CONTROL_X
LD005 ROT_CONTROL_Y
LD006 ROT_CONTROL_Z
LD007 ROT_X
LD008 ROT_Y
LD009 ROT_Z
LD010 WERKZEUG
LP000 NEW_ORIGIN
LP001 DIF_X
LP002 DIF_Y
LP003 XX
LP004 XY
LP005 P_CONTROL_OLD
LP006 P_CONTROL_NEW
LP007 MUL_OLD_NEW
//INST
///DATE 2023/02/06 14:27
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,0,11,0,0,8,0,0
NOP
SET OLD_UF 4
SET NEW_UF 4
SET WERKZEUG 0
SET ROT_X 0
SET ROT_Y 0
SET ROT_Z 0
CALL JOB:SET_TCPON ARGFWERKZEUG
TCPON TL#(WERKZEUG)
MUL ROT_X 10000
MUL ROT_Y 10000
MUL ROT_Z 10000
GETS PX100 $PX000
CNVRT PX100 PX100 UF#(OLD_UF)
SET NEW_ORIGIN P100
'Rotationen des neuen Ursprungs
'denen des alten Ursprungs
'anpassen (keine Rotation
'zwischen neuem und altem UF)
SETE NEW_ORIGIN (4) ROT_X
SETE NEW_ORIGIN (5) ROT_Y
SETE NEW_ORIGIN (6) ROT_Z
'X und Y des neuen Ursprungs
GETE X_NEW NEW_ORIGIN (1)
GETE Y_NEW NEW_ORIGIN (2)
'X_VERSATZ UF4 erzeugen
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFOLD_UF ARGFX_NEW ARGF0 ARGF0
SET DIF_X P100
'Y_VERSATZ UF4 erzeugen
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFOLD_UF ARGF0 ARGFY_NEW ARGF0
SET DIF_Y P100
MULMAT XX NEW_ORIGIN DIF_X
MULMAT XY NEW_ORIGIN DIF_Y
'Neues UF erzeugen
MFRAME UF#(NEW_UF) NEW_ORIGIN XX XY
'Kontrolle der Ausrichtung
'des neuen UF
'Controllpunkt OLD
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFOLD_UF ARGF0 ARGF0 ARGF0
SET P_CONTROL_OLD P100
SETE P_CONTROL_OLD (4) ROT_X
SETE P_CONTROL_OLD (5) ROT_Y
SETE P_CONTROL_OLD (6) ROT_Z
INVMAT P_CONTROL_OLD P_CONTROL_OLD
'Controllpunkt NEW
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFNEW_UF ARGF0 ARGF0 ARGF0
SET P_CONTROL_NEW P100
CNVRT P_CONTROL_NEW P_CONTROL_NEW UF#(OLD_UF)
MULMAT MUL_OLD_NEW P_CONTROL_OLD P_CONTROL_NEW
GETE ROT_CONTROL_X MUL_OLD_NEW (4)
GETE ROT_CONTROL_Y MUL_OLD_NEW (5)
GETE ROT_CONTROL_Z MUL_OLD_NEW (6)
IFTHENEXP ROT_CONTROL_X<>0 OREXP ROT_CONTROL_Y<>0 OREXP ROT_CONTROL_Z<>0
	 MSG "NEUES USERFRAME FALSCH ROTIERT!!"
	 SET ABBRUCH  EXPRESS -500000
	 MSG "NEUES USERFRAME FALSCH ROTIERT!!"
ENDIF
TCPOF
END
