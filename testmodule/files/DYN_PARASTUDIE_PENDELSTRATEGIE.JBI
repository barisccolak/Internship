/JOB
//NAME DYN_PARASTUDIE_PENDELSTRATEGIE
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,4,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D010 H1_LAGE_IST
D011 H2_LAGE_IST
///LVARS 0,5,18,0,1,8,0,0
LI000 PENDELRICHTUNG
LI001 PENDELANZAHL_S
LI002 PENDELANZAHL_I
LI003 LAGE_IST
LI004 LAGE_SOLL
LD000 Z
LD002 VERSATZ_MAX
LD003 PENDELBREITE
LD004 VERSATZ_MIN
LD005 KENNLINIE
LD006 V_WELD
LD007 VERSUCHSLAENGE
LD008 X
LD009 Y
LD010 V_MOVE
LD011 USERFRAME
LD013 Y_INKREMENT_POS
LD014 Y_INKREMENT_NEG
LD015 VERSATZ_ZUGABE
LD016 VERSATZ_IST
LD017 V_SCAN
LD018 SCANZUGABE
LD019 MESSEINZUG
LS000 VERSATZART
LP000 PUNKT_1
LP001 X_VERSATZ
LP002 Y_VERSATZ_POS
LP003 Y_VERSATZ_NEG
LP004 SCAN_START
LP005 SCAN_ENDE
LP006 GET_H1
LP007 GET_H2
//INST
///DATE 2021/09/14 09:15
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,5,30,0,1,8,0,0
NOP
'--------------------------------
'--------------------------------
'WID EINTRAGEN
'IDs Eingeben
CALL JOB:SET_ID ARGF440 ARGF"WID"
CALL JOB:SET_ID ARGF1 ARGF"NAHT_NR"
CALL JOB:SET_ID ARGF1 ARGF"LAGE_NR"
CALL JOB:SET_ID ARGF1 ARGF"RAUPE_NR"
'OFFSET
SET X 20000
SET Y 80000
SET Z 20000
'--------------------------------
'GEOMETRIE
SET VERSUCHSLAENGE 100000
SET PENDELBREITE 120000
SET VERSATZ_MIN 4300
SET VERSATZ_MAX 4300
SET USERFRAME 4
SET PENDELRICHTUNG 1
SET LAGE_SOLL 1
SET SCANZUGABE 15000
SET MESSEINZUG 10000
'VERSATZ VERGROESSERN ODER
'VERKLEINERN
SET VERSATZART "VERKLEINERN"
'--------------------------------
'SCHWEIPARAMETER
'--------------------------------
SET KENNLINIE 200
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'WELD-Geschwindigkeit
'       [cm/min]
CALL JOB:GET_V ARGF30 ARGF"cm/min"
GETS V_WELD $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_SCAN $RV
'Berechnungen
'--------------------------------
SET H1_GESAMT_IST Z
SET H2_GESAMT_IST Z
SET PENDELANZAHL_S  EXPRESS VERSUCHSLAENGE / (( VERSATZ_MIN + VERSATZ_MAX ) / 2 )
SET Y_INKREMENT_POS PENDELBREITE
SET Y_INKREMENT_NEG  EXPRESS PENDELBREITE * -1
'--------------------------------
'SAN_START ERZEUGEN
SUB X SCANZUGABE
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY ARGFZ
SET SCAN_START P100
ADD X SCANZUGABE
'--------------------------------
'GET_H1 ERZEUGEN
ADD X MESSEINZUG
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY ARGFZ
SET GET_H1 P100
SUB X MESSEINZUG
'--------------------------------
'ENDPUNKTE berechnen
ADD X VERSUCHSLAENGE
'--------------------------------
'SAN_ENDE ERZEUGEN
ADD X SCANZUGABE
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY ARGFZ
SET SCAN_ENDE P100
SUB X SCANZUGABE
'--------------------------------
'GET_H2 ERZEUGEN
SUB X MESSEINZUG
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY ARGFZ
SET GET_H2 P100
ADD X MESSEINZUG
'--------------------------------
SUB X VERSUCHSLAENGE
'--------------------------------
'PUNKT_1 erzeugen
IFTHENEXP PENDELRICHTUNG=1
	 SET Y  EXPRESS Y - PENDELBREITE / 2
ELSEIFEXP PENDELRICHTUNG=-1
	 SET Y  EXPRESS Y + PENDELBREITE / 2
ENDIF
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX ARGFY ARGFH1_GESAMT_IST
SET PUNKT_1 P100
'--------------------------------
'X_VERSATZ
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF0 ARGF0 ARGF0
SET X_VERSATZ P100
'--------------------------------
'Y_VERSATZ_POS
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF0 ARGFY_INKREMENT_POS ARGF0
SET Y_VERSATZ_POS P100
'--------------------------------
'Y_VERSATZ_NEG
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF0 ARGFY_INKREMENT_NEG ARGF0
SET Y_VERSATZ_NEG P100
'--------------------------------
SET LAGE_IST 0
'--------------------------------
'Beginn der Fertigung
'--------------------------------
'--------------------------------
WHILEEXP LAGE_IST<LAGE_SOLL
	 ADD LAGE_IST 1
	 SET VERSATZ_ZUGABE  EXPRESS ( VERSATZ_MAX - VERSATZ_MIN ) / ( PENDELANZAHL_S - 1 )
	 IFTHENEXP VERSATZART="VERKLEINERN"
		 SET VERSATZ_IST VERSATZ_MAX
		 MUL VERSATZ_ZUGABE -1
	 ELSEIFEXP VERSATZART="VERGROESSERN"
		 SET VERSATZ_IST VERSATZ_MIN
	 ENDIF
	'H1 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H2 Messen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'--------------------------------
	 SETE PUNKT_1 (3) H1_GESAMT_IST
	 IFTHENEXP H1_GESAMT_IST>=H2_GESAMT_IST
		 SETE SCAN_START (3) H1_GESAMT_IST
		 SETE SCAN_ENDE (3) H1_GESAMT_IST
	 ELSE
		 SETE SCAN_START (3) H2_GESAMT_IST
		 SETE SCAN_ENDE (3) H2_GESAMT_IST
	 ENDIF
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	'-----------SCHWEISSEN-----------
	 TCPON TL#(0)
	 MOVL PUNKT_1 V=V_MOVE
	'--------------------------------
	 SET PENDELANZAHL_I 0
	 WHILEEXP PENDELANZAHL_I<=PENDELANZAHL_S
		'Berechnung des z-Inkrementes
		 SET Z  EXPRESS ( H2_GESAMT_IST - H1_GESAMT_IST ) * VERSATZ_IST / ( VERSUCHSLAENGE )
		 SETE X_VERSATZ (3) Z
		 SETE X_VERSATZ (1) VERSATZ_IST
		 ADD PENDELANZAHL_I 1
		 IFTHENEXP PENDELRICHTUNG=1
			 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
			 CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
			 CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
			 TIMER T=1.00
			 CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
			 ARCON ASF#(KENNLINIE)
			 IMOV Y_VERSATZ_POS V=V_WELD UF#(USERFRAME)
			 ARCOF AEF#(KENNLINIE)
			 CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
			 IMOV X_VERSATZ V=V_MOVE UF#(USERFRAME)
			 IMOV Y_VERSATZ_NEG V=V_MOVE UF#(USERFRAME)
			 CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
			 CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
			 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
		 ELSEIFEXP PENDELRICHTUNG=-1
			 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
			 CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
			 CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
			 TIMER T=1.00
			 CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
			 ARCON ASF#(KENNLINIE)
			 IMOV Y_VERSATZ_NEG V=V_WELD UF#(USERFRAME)
			 ARCOF AEF#(KENNLINIE)
			 CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
			 IMOV X_VERSATZ V=V_MOVE UF#(USERFRAME)
			 IMOV Y_VERSATZ_POS V=V_MOVE UF#(USERFRAME)
			 CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
			 CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
			 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
		 ENDIF
		 ADD VERSATZ_IST VERSATZ_ZUGABE
		'Zischenraupenzeit
		 TIMER T=2.00
	 ENDWHILE
	 TCPOF
	'------------SCANNEN-------------
	 CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
	 TCPON TL#(1)
	 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
	 MOVL SCAN_START V=V_MOVE
	 CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
	 TIMER T=0.50
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EXT"
	 TIMER T=0.50
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 DOUT OT#(1) ON
	 MOVL SCAN_ENDE V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
	 TIMER T=0.50
	 CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
	 TIMER T=0.50
	 CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
	 DOUT OT#(1) OFF
	 TIMER T=1.00
	 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
	 TCPOF
	'STARTPUNKT ALTERNIEREN
	 IFTHENEXP PENDELRICHTUNG=1
		 SET PENDELRICHTUNG -1
		 ADD Y PENDELBREITE
	 ELSEIFEXP PENDELRICHTUNG=-1
		 SET PENDELRICHTUNG 1
		 SUB Y PENDELBREITE
	 ENDIF
	 SETE PUNKT_1 (2) Y
ENDWHILE
'--------------------------------
END
