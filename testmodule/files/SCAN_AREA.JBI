/JOB
//NAME SCAN_AREA
///FOLDERNAME SCAN_JOBS
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 1,2,13,0,0,2,0,0
LB000 CALLED_IN_MAIN
LI000 SCAN_SOLL
LI001 SCAN_IST
LD000 USERFRAME
LD001 X_1_AREA
LD002 Y_1_AREA
LD003 Z_1_AREA
LD004 LENGTH_AREA
LD005 WIDTH_AREA
LD006 V_SCAN
LD007 X_1_SCAN
LD008 X_2_SCAN
LD009 Y_SCAN
LD010 WIDTH_SCAN
LD011 V_MOVE
LD012 SCANZUGABE
LP000 SCAN_1
LP001 SCAN_2
//ARGINFO
///ARGTYPE D,D,D,D,D,D,B,
///COMMENT
USERFRAME
X_1_AREA
Y_1_AREA
Z_1_AREA
LENGTH_AREA
WIDTH_AREA
CALLED_IN_MAIN

//INST
///DATE 2023/02/16 15:31
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,2,13,0,0,2,0,0
NOP
GETARG USERFRAME IARG#(1)
GETARG X_1_AREA IARG#(2)
GETARG Y_1_AREA IARG#(3)
GETARG Z_1_AREA IARG#(4)
GETARG LENGTH_AREA IARG#(5)
GETARG WIDTH_AREA IARG#(6)
GETARG CALLED_IN_MAIN IARG#(7)
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF20 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'--------------------------------
SET WIDTH_SCAN 80000
SET SCANZUGABE 20000
'KOORDINATEN BERECHNEN
SET X_1_SCAN  EXPRESS X_1_AREA - SCANZUGABE
SET X_2_SCAN  EXPRESS X_1_AREA + LENGTH_AREA + SCANZUGABE
SET Y_SCAN  EXPRESS Y_1_AREA + WIDTH_SCAN / 2
'--------------------------------
'PUNKTE ERZEUGEN
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1_SCAN ARGFY_SCAN ARGFZ_1_AREA
SET SCAN_1 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2_SCAN ARGFY_SCAN ARGFZ_1_AREA
SET SCAN_2 P100
SET SCAN_SOLL  EXPRESS WIDTH_AREA / ( WIDTH_SCAN )
SET SCAN_IST 0
'--------------------------------
'SCANNEN
CALL JOB:SET_TCPON ARGF1
TCPON TL#(1)
IFTHENEXP CALLED_IN_MAIN=0
	 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
ENDIF
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EXT"
TIMER T=0.50
WHILEEXP SCAN_IST<=SCAN_SOLL
	 MOVL SCAN_1 V=V_MOVE
	 TIMER T=0.50
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 MOVL SCAN_2 V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
	'Y KOMP DER SCANPKT. SHIFTEN
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_1 ARGF"y" ARGFWIDTH_SCAN
	 SET SCAN_1 P100
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_2 ARGF"y" ARGFWIDTH_SCAN
	 SET SCAN_2 P100
	 ADD SCAN_IST 1
ENDWHILE
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TIMER T=3.00
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
IFTHENEXP CALLED_IN_MAIN=0
	 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
ENDIF
END
