/JOB
//NAME SHIFT_PKT_ELEMENT
///FOLDERNAME INTERNE_BERECHNUNGEN
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,0,1,0,1,3,0,0
LD000 SHIFT_WERT
LS000 SHIFT_ELEMENT
LP000 SHIFT_PKT
LP001 PKT_IST
LP002 PKT_SOLL
//ARGINFO
///ARGTYPE P,S,D,,,,,
///COMMENT
Shiftpunkt
Shiftelement
Shiftwert





//INST
///DATE 2021/10/11 09:26
///ATTR SC,RW
///LVARS 0,0,1,0,1,3,0,0
NOP
'--------------------------------
'Dieser Job shiftet ein Element
'(2.Argument) des im ersten
'Argument angegebenen Punktes um
'den im 3. Argument angegebenen
'Wert
'--------------------------------
'--------------------------------
'Einlesen der Argumente
'--------------------------------
GETARG PKT_IST IARG#(1)
GETARG SHIFT_ELEMENT IARG#(2)
GETARG SHIFT_WERT IARG#(3)
'--------------------------------
'Lokalen Shiftpunkt erzeugen und
'alle Einträge auf 0 setzen
SET SHIFT_PKT PKT_IST
SUB SHIFT_PKT SHIFT_PKT
'--------------------------------
'Shiftwerte übernehemen
IFTHENEXP SHIFT_ELEMENT="x" OREXP SHIFT_ELEMENT="X"
	 SETE SHIFT_PKT (1) SHIFT_WERT
ELSEIFEXP SHIFT_ELEMENT="y" OREXP SHIFT_ELEMENT="Y"
	 SETE SHIFT_PKT (2) SHIFT_WERT
ELSEIFEXP SHIFT_ELEMENT="z" OREXP SHIFT_ELEMENT="Z"
	 SETE SHIFT_PKT (3) SHIFT_WERT
ELSEIFEXP SHIFT_ELEMENT="rx" OREXP SHIFT_ELEMENT="rX" OREXP SHIFT_ELEMENT="Rx" OREXP SHIFT_ELEMENT="RX"
	 SETE SHIFT_PKT (4) SHIFT_WERT
ELSEIFEXP SHIFT_ELEMENT="ry" OREXP SHIFT_ELEMENT="rY" OREXP SHIFT_ELEMENT="Ry" OREXP SHIFT_ELEMENT="RY"
	 SETE SHIFT_PKT (5) SHIFT_WERT
ELSEIFEXP SHIFT_ELEMENT="rz" OREXP SHIFT_ELEMENT="rZ" OREXP SHIFT_ELEMENT="Rz" OREXP SHIFT_ELEMENT="RZ"
	 SETE SHIFT_PKT (6) SHIFT_WERT
ELSE
	 MSG "FALSCHE EING SHIFT_PUNKT_ELEMENT"
	 SET B000  EXPRESS -500000
ENDIF
'Lokalen Punkt PKT_SOLL erzeugen
'und alle Einträge auf 0 setzen
SET PKT_SOLL PKT_IST
ADD PKT_SOLL SHIFT_PKT
'--------------------------------
'globalen Punkt P100 aus lokalem
'Punkt PKT_SOLL erzeugen
'P100 kann aus dem aufrufenden
'JOB eingelesen werden
SET P100 PKT_SOLL
END
