/JOB
//NAME GET_LAYER_HEIGHT_ARG_P
///FOLDERNAME TWINCAT_KOMMUNIKATION
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 0,0,4,0,0,2,0,0
LD000 V_MOVE
LD001 Z_ROT_ADD
LD002 Z_ROT_IST
LD003 LAYER_HEIGHT
LP000 GET_HEIGHT
LP001 Z_INKREMENT
//ARGINFO
///ARGTYPE P,D,,,,,,
///COMMENT
MESSPUNK
Z_ROTATION






//INST
///DATE 2023/07/20 07:40
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,1,7,0,0,2,0,0
NOP
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'
'################################
'Argumente einlesen
'--------------------------------
GETARG GET_HEIGHT IARG#(1)
GETARG Z_ROT_ADD IARG#(2)
'################################
'Messpunkt erzeugen
GETE Z_ROT_IST GET_HEIGHT (6)
ADD Z_ROT_IST Z_ROT_ADD
SETE GET_HEIGHT (6) Z_ROT_IST
'--------------------------------
SET Z_INKREMENT GET_HEIGHT
SETE Z_INKREMENT (1) 0
SETE Z_INKREMENT (2) 0
SETE Z_INKREMENT (3) 100000
'################################
'-----  LAGENHOEHE MESSEN  -----'
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
CALL JOB:SET_TCPON ARGF1
TCPON TL#(1)
TIMER T=0.20
*MESSUNG
MOVL GET_HEIGHT V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=1.00
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS LAYER_HEIGHT $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
IFTHENEXP LAYER_HEIGHT<-15000 OREXP LAYER_HEIGHT>15000
	 IMOV Z_INKREMENT V=V_MOVE TF
	 MSG "Fehler am LLT!"
	 INPUT B000
	 JUMP *MESSUNG
ENDIF
RET LAYER_HEIGHT
END
