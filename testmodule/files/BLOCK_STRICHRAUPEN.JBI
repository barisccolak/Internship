/JOB
//NAME BLOCK_STRICHRAUPEN
///FOLDERNAME STRICHRAUPEN
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,8,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D002 H3_GESAMT_IST
D003 H4_GESAMT_IST
D010 H1_LAGE_IST
D011 H2_LAGE_IST
D012 H3_LAGE_IST
D013 H4_LAGE_IST
///LVARS 0,2,20,0,0,8,0,0
LI000 LAGE_IST
LI001 RAUPENANZAHL_S
LD000 OFFSET_Z
LD001 VERSATZ
LD002 STARTWAHL
LD003 BLOCKBREITE
LD004 MESSEINZUG
LD005 KENNLINIE
LD006 V_WELD
LD007 VERSUCHSLAENGE
LD008 OFFSET_X
LD009 OFFSET_Y
LD010 V_MOVE
LD011 USERFRAME
LD012 V_SCAN
LD013 X_1
LD014 X_2
LD015 Y_1
LD016 Y_2
LD017 H_MIN
LD018 H_SOLL
LD019 Z_ROTATION
LP000 Pkt_1
LP001 GET_H1
LP002 GET_H2
LP003 GET_H3
LP004 GET_H4
LP005 Pkt_2
LP006 Pkt_3
LP007 Pkt_4
//INST
///DATE 2022/10/05 09:05
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,2,20,0,0,10,0,0
NOP
CALL JOB:TRIGGER_RESET
'ID'S EINLESEN
CALL JOB:SET_ID ARGF492 ARGF"WID"
CALL JOB:SET_ID ARGF3 ARGF"NAHT_NR"
CALL JOB:SET_ID ARGF1 ARGF"LAGE_NR"
CALL JOB:SET_ID ARGF1 ARGF"RAUPE_NR"
'--------------------------------
'OFFSET
SET OFFSET_X 65000
SET OFFSET_Y 65000
SET OFFSET_Z 0
'--------------------------------
'GEOMETRIE
SET VERSUCHSLAENGE 50000
SET BLOCKBREITE 25000
SET USERFRAME 4
SET H_SOLL 40000
SET MESSEINZUG 15000
'--------------------------------
'GESCHWINDIGKEITEN
'MOVE-GESCHWINDIGKEIT
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'WELD-GESCHWINDIGKEIT
'       [cm/min]
CALL JOB:GET_V ARGF30 ARGF"cm/min"
GETS V_WELD $RV
'--------------------------------
'SCAN-GESCHWINDIGKEIT
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'USERFRAME IN TC SETZEN
CALL JOB:SET_UFRAME ARGFUSERFRAME
'BERECHNUNGEN
'--------------------------------
SET H1_GESAMT_IST OFFSET_Z
SET H2_GESAMT_IST OFFSET_Z
SET H3_GESAMT_IST OFFSET_Z
SET H4_GESAMT_IST OFFSET_Z
SET H_MIN OFFSET_Z
SET Z_ROTATION -900000
'--------------------------------
'FERTIGUNGSBEGINN
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
'--------------------------------
CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'--------------------------------
WHILEEXP H_MIN<=H_SOLL
	'LAGE AUS TC HOLEN
	 CALL JOB:GET_ID ARGF"LAGE_NR"
	 GETS LAGE_IST $RV
	 TIMER T=0.50
	'--------------------------------
	 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
	'--------------------------------
	'SCHWEIPARAMETER
	'--------------------------------
	'WAEHLT KENNLINIE DER LAGE
	 CALL JOB:KENNLINIENWAHL ARGF71 ARGF72 ARGF73 ARGF74 ARGFLAGE_IST
	 GETS KENNLINIE $RV
	'WAEHLT VERSATZ DER LAGE
	 CALL JOB:KENNLINIENWAHL ARGF4900 ARGF4750 ARGF4140 ARGF4500 ARGFLAGE_IST
	 GETS VERSATZ $RV
	'--------------------------------
	'PUNKTKOORDINATEN BERECHNEN ECKEN
	 SET RAUPENANZAHL_S  EXPRESS VERSUCHSLAENGE / ( VERSATZ ) + 1
	 SET X_1  EXPRESS OFFSET_X - VERSATZ * RAUPENANZAHL_S * 0.5
	 SET X_2  EXPRESS OFFSET_X + VERSATZ * RAUPENANZAHL_S * 0.5
	 SET Y_1  EXPRESS OFFSET_Y - BLOCKBREITE * 0.5
	 SET Y_2  EXPRESS OFFSET_Y + BLOCKBREITE * 0.5
	'--------------------------------
	'ECKPUNKTE ERZEUGEN
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_1 ARGFOFFSET_Z
	 SET Pkt_1 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_2 ARGFOFFSET_Z
	 SET Pkt_2 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_1 ARGFOFFSET_Z
	 SET Pkt_3 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_2 ARGFOFFSET_Z
	 SET Pkt_4 P100
	'z-ROTATIONEN HINZUFUEGEN
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFPkt_1 ARGF"Rz" ARGFZ_ROTATION
	 SET Pkt_1 P100
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFPkt_2 ARGF"Rz" ARGFZ_ROTATION
	 SET Pkt_2 P100
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFPkt_3 ARGF"Rz" ARGFZ_ROTATION
	 SET Pkt_3 P100
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFPkt_4 ARGF"Rz" ARGFZ_ROTATION
	 SET Pkt_4 P100
	'PUNKTE ZUR HOEHENMESSUNG
	'ERZEUGEN (AUSSEN)
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H1 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H4 P100
	'--------------------------------
	'PKTKOORD BERECHNEN HOEHENMESSUNG
	 ADD X_1 MESSEINZUG
	 SUB X_2 MESSEINZUG
	'PUNKTE ZUR HOEHENMESSUNG
	'ERZEUGEN (INNEN)
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H2 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H3 P100
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'H1 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H2 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'H3 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H3 ARGF0
	 GETS H3_LAGE_IST $RV
	 ADD H3_GESAMT_IST H3_LAGE_IST
	'H4 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H4 ARGF0
	 GETS H4_LAGE_IST $RV
	 ADD H4_GESAMT_IST H4_LAGE_IST
	'MINIMALE HOEHE VON H1 BIS H4
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'GEM. HOEHEN IN PUNKTE SCHREIBEN
	 SETE Pkt_1 (3) H1_GESAMT_IST
	 SETE Pkt_2 (3) H1_GESAMT_IST
	 SETE Pkt_3 (3) H4_GESAMT_IST
	 SETE Pkt_4 (3) H4_GESAMT_IST
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'--------------------------------
	'SCANNEN ---> LAGE
	 CALL JOB:SCAN_LINEAR_ARG_P ARGFGET_H1 ARGFGET_H4 ARGF15000 ARGFV_SCAN
	 TIMER T=5.00
	'-----------SCHWEISSEN-----------
	'--------------------------------
	'WELCHE STRATEGIE SOLL
	'GESCHWEISST WERDEN
	 SET STARTWAHL LAGE_IST
	 WHILEEXP STARTWAHL>0
		 SUB STARTWAHL 4
	 ENDWHILE
	 SET D025 STARTWAHL
	'--------------------------------
	'AUFRUF DER VERSCHIEDENEN START -
	'STRATEGIEN WELD-STRICHRAUPEN
	 IFTHENEXP STARTWAHL=-3
		'STRATEGIE 1
		 CALL JOB:WELD_STRICHRAUPEN ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=-2
		'STRATEGIE 2
		 CALL JOB:WELD_STRICHRAUPEN ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=-1
		'STRATEGIE 3
		 CALL JOB:WELD_STRICHRAUPEN ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=0
		'STRATEGIE 4
		 CALL JOB:WELD_STRICHRAUPEN ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ENDIF
	'HOEHEN ERNEUT VERMESSEN
	'H1 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	'SET H1_LAGE_IST 0
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H2 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	'SET H2_LAGE_IST 300
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'H3 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H3 ARGF0
	 GETS H3_LAGE_IST $RV
	'SET H3_LAGE_IST 400
	 ADD H3_GESAMT_IST H3_LAGE_IST
	'H4 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H4 ARGF0
	 GETS H4_LAGE_IST $RV
	'SET H4_LAGE_IST 50
	 ADD H4_GESAMT_IST H4_LAGE_IST
	'MINIMALE HOEHE VON H1 BIS H4
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'GEM. HOEHEN IN PUNKTE SCHREIBEN
	 SETE Pkt_1 (3) H1_GESAMT_IST
	 SETE Pkt_2 (3) H1_GESAMT_IST
	 SETE Pkt_3 (3) H4_GESAMT_IST
	 SETE Pkt_4 (3) H4_GESAMT_IST
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'--------------------------------
	'AUFRUF DER VERSCHIEDENEN START -
	'STRATEGIEN RAISE-FALLING-EDGE
	 IFTHENEXP STARTWAHL=0
		'STRATEGIE 1
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 CALL JOB:RAISE_FALLING_EDGE_STRICHRAUPEN ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_2 (3) H1_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-1
		'STRATEGIE 2
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 CALL JOB:RAISE_FALLING_EDGE_STRICHRAUPEN ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_1 (3) H1_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-2
		'STRATEGIE 3
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 CALL JOB:RAISE_FALLING_EDGE_STRICHRAUPEN ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-3
		'STRATEGIE 4
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 CALL JOB:RAISE_FALLING_EDGE_STRICHRAUPEN ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
	 ENDIF
	 TIMER T=1.00
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'ZWISCHENLAGENZEIT
	 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
	 TIMER T=10.00
	'--------------------------------
	'LAGE WIRD IN TC UM 1 ERHOEHT
	' --> R=1, S=0
	 CALL JOB:INC_NR ARGF"LAGE_NR"
ENDWHILE
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
