/JOB
//NAME TAKE_PICTURES_OF_LINEAR_TRACE
///FOLDERNAME SCAN_JOBS
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,0,9,1,0,4,0,0
LD000 INKREMENTE_S
LD001 V_MOVE
LD002 X_INKR
LD003 Y_INKR
LD004 Z_INKR
LD005 PICT_DIST
LD008 BETRAG_DOUB
LD009 INKREMENTE_I
LD010 USERFRAME
LR000 BETRAG_REAL
LP000 PKT_1
LP001 PKT_2
LP002 PKT_INKR
LP003 SAFE_POINT
//ARGINFO
///ARGTYPE P,P,D,D,,,,
///COMMENT
STARTPUNKT
ENDPUNKT
USERFRAME
PICTURE_DIST




//INST
///DATE 2022/11/08 14:31
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,1,11,1,0,4,0,0
NOP
'ARGUMENTE EINLESEN
GETARG PKT_1 IARG#(1)
GETARG PKT_2 IARG#(2)
GETARG USERFRAME IARG#(3)
GETARG PICT_DIST IARG#(4)
'MOVE-GESCHWINDIGKEIT
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'SAFEPOINT 100mm UEBER
'AKTUELLER POSITION
GETS PX100 $PX000
CNVRT PX100 PX100 UF#(USERFRAME)
SET SAFE_POINT P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSAFE_POINT ARGF"z" ARGF100000
SET SAFE_POINT P100
MOVL SAFE_POINT V=V_MOVE
'INKREMENTALPUNKT BERECHNEN
SET PKT_INKR PKT_2
SUB PKT_INKR PKT_1
'EINZELKOMPONENTEN AUSLESEN
GETE X_INKR PKT_INKR (1)
GETE Y_INKR PKT_INKR (2)
GETE Z_INKR PKT_INKR (3)
'BETRAG BERECHNEN
SET BETRAG_REAL  EXPRESS X_INKR * X_INKR + Y_INKR * Y_INKR + Z_INKR * Z_INKR
SQRT BETRAG_REAL BETRAG_REAL
SET BETRAG_DOUB BETRAG_REAL
'INKREMENTANZAHL BERECHNEN
SET INKREMENTE_S  EXPRESS BETRAG_DOUB / ( PICT_DIST ) + 1
'INKREMENTALPUNKT SKALIEREN
DIV PKT_INKR INKREMENTE_S
SET INKREMENTE_I 0
'BEWEGUNGSABLAUF
TCPON TL#(8)
MOVL PKT_1 V=V_MOVE
CALL JOB:TRIG_CAM
WHILEEXP INKREMENTE_I<INKREMENTE_S
	 IMOV PKT_INKR V=V_MOVE UF#(USERFRAME)
	 CALL JOB:TRIG_CAM
	 ADD INKREMENTE_I 1
ENDWHILE
TCPOF
END
