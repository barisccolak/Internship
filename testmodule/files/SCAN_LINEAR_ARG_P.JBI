/JOB
//NAME SCAN_LINEAR_ARG_P
///FOLDERNAME SCAN_JOBS
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,0,7,0,0,3,0,0
LD000 SCAN_INK_X
LD001 SCAN_INK_Y
LD002 V_SCAN
LD003 SCAN_H1
LD004 SCAN_H2
LD005 V_MOVE
LD006 SCANZUGABE
LP000 SCAN_P1
LP001 SCAN_P2
LP002 SCAN_INKREMENT
//ARGINFO
///ARGTYPE P,P,D,D,,,,
///COMMENT
SCANPKT 1
SCANPKT 2
SCANZUGABE
V_SCAN




//INST
///DATE 2020/12/07 12:32
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,0,10,0,0,3,0,0
NOP
'Argumente einlesen
GETARG SCAN_P1 IARG#(1)
GETARG SCAN_P2 IARG#(2)
GETARG SCANZUGABE IARG#(3)
GETARG V_SCAN IARG#(4)
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'PUNKTKOORDINATEN EINLESEN
GETE SCAN_H1 SCAN_P1 (3)
GETE SCAN_H2 SCAN_P2 (3)
'PUNKHOEHEN ZUWEISEN
IFTHENEXP SCAN_H1>SCAN_H2
	 SETE SCAN_P2 (3) SCAN_H1
ELSEIFEXP SCAN_H2>SCAN_H1
	 SETE SCAN_P1 (3) SCAN_H2
ENDIF
'SCANZUGABE HINZUFUEGEN
SET SCAN_INKREMENT SCAN_P2
SUB SCAN_INKREMENT SCAN_P1
GETE SCAN_INK_X SCAN_INKREMENT (1)
GETE SCAN_INK_Y SCAN_INKREMENT (2)
IFTHENEXP SCAN_INK_X=0 ANDEXP SCAN_INK_Y>0
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P2 ARGF"y" ARGFSCANZUGABE
	 SET SCAN_P2 P100
	 MUL SCANZUGABE -1
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P1 ARGF"y" ARGFSCANZUGABE
	 SET SCAN_P1 P100
ELSEIFEXP SCAN_INK_X=0 ANDEXP SCAN_INK_Y<0
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P1 ARGF"y" ARGFSCANZUGABE
	 SET SCAN_P1 P100
	 MUL SCANZUGABE -1
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P2 ARGF"y" ARGFSCANZUGABE
	 SET SCAN_P2 P100
ELSEIFEXP SCAN_INK_X>0 ANDEXP SCAN_INK_Y=0
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P2 ARGF"x" ARGFSCANZUGABE
	 SET SCAN_P2 P100
	 MUL SCANZUGABE -1
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P1 ARGF"x" ARGFSCANZUGABE
	 SET SCAN_P1 P100
ELSEIFEXP SCAN_INK_X<0 ANDEXP SCAN_INK_Y=0
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P1 ARGF"x" ARGFSCANZUGABE
	 SET SCAN_P1 P100
	 MUL SCANZUGABE -1
	 CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_P2 ARGF"x" ARGFSCANZUGABE
	 SET SCAN_P2 P100
ENDIF
CALL JOB:SET_TCPON ARGF1
TCPON TL#(1)
MOVL SCAN_P1 V=V_MOVE
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EXT"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
MOVL SCAN_P2 V=V_SCAN
CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
TCPOF
END
