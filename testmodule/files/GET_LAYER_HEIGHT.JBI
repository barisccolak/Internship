/JOB
//NAME GET_LAYER_HEIGHT
///FOLDERNAME TWINCAT_KOMMUNIKATION
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,1,7,0,0,1,0,0
LI000 USERFRAME
LD000 X_COORD
LD001 Y_COORD
LD002 Z_COORD
LD003 V_MOVE
LD004 Z_ROT_ADD
LD005 Z_ROT_IST
LD006 LAYER_HEIGHT
LP000 GET_HEIGHT
//ARGINFO
///ARGTYPE I,D,D,D,D,,,
///COMMENT
USERFRAME
X_KOORDINATE
Y_KOORDINATE
Z_KOORDINATE
Z_ROTATION



//INST
///DATE 2023/06/22 13:06
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,1,7,0,0,2,0,0
NOP
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF60 ARGF"mm/s"
GETS V_MOVE $RV
'
'################################
'Argumente einlesen
'--------------------------------
GETARG USERFRAME IARG#(1)
GETARG X_COORD IARG#(2)
GETARG Y_COORD IARG#(3)
GETARG Z_COORD IARG#(4)
GETARG Z_ROT_ADD IARG#(5)
'################################
'Messpunkt erzeugen
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_COORD ARGFY_COORD ARGFZ_COORD
SET GET_HEIGHT P100
GETE Z_ROT_IST GET_HEIGHT (6)
ADD Z_ROT_IST Z_ROT_ADD
SETE GET_HEIGHT (6) Z_ROT_IST
'################################
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
'--------------------------------
'################################
'-----  LAGENHOEHE MESSEN  -----'
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPON TL#(1)
TIMER T=0.20
MOVL GET_HEIGHT V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.20
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS LAYER_HEIGHT $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
IFTHENEXP LAYER_HEIGHT<-15000
	 MSG "Fehler am Lichtschnittsensor!"
	 SET B000  EXPRESS -500000
ENDIF
RET LAYER_HEIGHT
END
