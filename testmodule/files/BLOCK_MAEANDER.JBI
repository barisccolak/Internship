/JOB
//NAME BLOCK_MAEANDER
///FOLDERNAME BLOCK_MAEANDER
//POS
///NPOS 0,0,0,5,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00001=-203.350,-49.507,11.578,0.0000,0.0000,0.0000
P00002=-198.450,-47.843,11.611,0.0000,0.0000,0.0000
P00003=-198.450,-73.269,11.611,0.0000,0.0000,0.0000
P00004=-193.550,-71.576,11.644,0.0000,0.0000,0.0000
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,8,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D002 H3_GESAMT_IST
D003 H4_GESAMT_IST
D010 H1_LAGE_IST
D011 H2_LAGE_IST
D012 H3_LAGE_IST
D013 H4_LAGE_IST
///LVARS 0,1,21,0,1,8,0,0
LI000 LAGE_IST
LD000 OFFSET_Z
LD001 VERSATZ
LD002 STARTWAHL
LD003 BLOCKBREITE
LD004 MESSEINZUG
LD005 KENNLINIE
LD006 V_WELD
LD007 BLOCKLAENGE
LD008 OFFSET_X
LD009 OFFSET_Y
LD010 V_MOVE
LD011 USERFRAME
LD012 V_SCAN
LD013 X_1
LD014 X_2
LD015 Y_1
LD016 Y_2
LD017 H_MIN
LD018 H_SOLL
LD019 RAUPENANZAHL_S
LD020 VERSATZANZAHL
LS000 RFE_DIR
LP000 Pkt_1
LP001 GET_H1
LP002 GET_H2
LP003 GET_H3
LP004 GET_H4
LP005 Pkt_2
LP006 Pkt_3
LP007 Pkt_4
//INST
///DATE 2023/08/08 15:53
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,1,21,0,1,8,0,0
NOP
CALL JOB:TRIGGER_RESET
'ID'S SETZEN
CALL JOB:SET_IDS_FULL ARGF511 ARGF7 ARGF4 ARGF1 ARGF0
'--------------------------------
'OFFSET
SET OFFSET_X -50000
SET OFFSET_Y -170000
SET OFFSET_Z 8000
'--------------------------------
'GEOMETRIE
SET BLOCKLAENGE 50000
SET BLOCKBREITE 20000
SET USERFRAME 4
SET H_SOLL 40000
SET MESSEINZUG 35000
'--------------------------------
'USERFRAME in TwinCAT setzen
CALL JOB:SET_UFRAME ARGFUSERFRAME
'--------------------------------
'GESCHWINDIGKEITEN
'MOVE-GESCHWINDIGKEIT
'       [mm/s]
CALL JOB:GET_V ARGF400 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'WELD-GESCHWINDIGKEIT
'       [cm/min]
CALL JOB:GET_V ARGF57 ARGF"cm/min"
GETS V_WELD $RV
'--------------------------------
'SCAN-GESCHWINDIGKEIT
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'RAISE FALLING EDGE DIRECTION
SET RFE_DIR "OUTWARDS"
'BERECHNUNGEN
'--------------------------------
SET H1_GESAMT_IST OFFSET_Z
SET H2_GESAMT_IST OFFSET_Z
SET H3_GESAMT_IST OFFSET_Z
SET H4_GESAMT_IST OFFSET_Z
SET H_MIN OFFSET_Z
'--------------------------------
'FERTIGUNGSBEGINN
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
'--------------------------------
COMM CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'--------------------------------
WHILEEXP H_MIN<=H_SOLL
	'LAGE AUS TC HOLEN
	 CALL JOB:GET_ID ARGF"LAGE_NR"
	 GETS LAGE_IST $RV
	 TIMER T=0.50
	'--------------------------------
	 CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
	'--------------------------------
	'SCHWEIPARAMETER
	'--------------------------------
	'WAEHLT KENNLINIE DER LAGE
	 CALL JOB:CHOOSE_ARG_BY_LAYER ARGF71 ARGF72 ARGF73 ARGF74 ARGFLAGE_IST
	 GETS KENNLINIE $RV
	'WAEHLT VERSATZ DER LAGE
	 CALL JOB:CHOOSE_ARG_BY_LAYER ARGF4900 ARGF4750 ARGF4140 ARGF4300 ARGFLAGE_IST
	 GETS VERSATZ $RV
	'--------------------------------
	'PUNKTKOORDINATEN BERECHNEN ECKEN
	 SET RAUPENANZAHL_S  EXPRESS BLOCKLAENGE / ( VERSATZ ) + 1
	 CALL JOB:GET_EVEN_NUMBER ARGFRAUPENANZAHL_S ARGF"UP"
	 GETS RAUPENANZAHL_S $RV
	 SET VERSATZANZAHL  EXPRESS RAUPENANZAHL_S - 1
	 SET X_1  EXPRESS OFFSET_X - VERSATZ * VERSATZANZAHL * 0.5
	 SET X_2  EXPRESS OFFSET_X + VERSATZ * VERSATZANZAHL * 0.5
	 SET Y_1  EXPRESS OFFSET_Y - BLOCKBREITE * 0.5
	 SET Y_2  EXPRESS OFFSET_Y + BLOCKBREITE * 0.5
	'--------------------------------
	'ECKPUNKTE ERZEUGEN
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_1 ARGFOFFSET_Z
	 SET Pkt_1 P100
	 SET P001 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_2 ARGFOFFSET_Z
	 SET Pkt_2 P100
	 SET P002 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_1 ARGFOFFSET_Z
	 SET Pkt_3 P100
	 SET P003 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_2 ARGFOFFSET_Z
	 SET Pkt_4 P100
	 SET P004 P100
	'PUNKTE ZUR HOEHENMESSUNG
	'ERZEUGEN (AUSSEN)
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H1 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H4 P100
	'--------------------------------
	'PKTKOORD BERECHNEN HOEHENMESSUNG
	 ADD X_1 MESSEINZUG
	 SUB X_2 MESSEINZUG
	'PUNKTE ZUR HOEHENMESSUNG
	'ERZEUGEN (INNEN)
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H2 P100
	 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFOFFSET_Z
	 SET GET_H3 P100
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'H1 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H2 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'H3 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H3 ARGF0
	 GETS H3_LAGE_IST $RV
	 ADD H3_GESAMT_IST H3_LAGE_IST
	'H4 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H4 ARGF0
	 GETS H4_LAGE_IST $RV
	 ADD H4_GESAMT_IST H4_LAGE_IST
	'MINIMALE HOEHE VON H1 BIS H4
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'GEM. HOEHEN IN PUNKTE SCHREIBEN
	 SETE Pkt_1 (3) H1_GESAMT_IST
	 SETE Pkt_2 (3) H1_GESAMT_IST
	 SETE Pkt_3 (3) H4_GESAMT_IST
	 SETE Pkt_4 (3) H4_GESAMT_IST
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'--------------------------------
	'SCANNEN ---> SUBSTRAT
	 CALL JOB:SCAN_LINEAR_ARG_P ARGFGET_H1 ARGFGET_H4 ARGF15000 ARGFV_SCAN
	 TIMER T=5.00
	'-----------SCHWEISSEN-----------
	'--------------------------------
	'WELCHE STRATEGIE SOLL
	'GESCHWEISST WERDEN
	 SET STARTWAHL LAGE_IST
	 WHILEEXP STARTWAHL>0
		 SUB STARTWAHL 4
	 ENDWHILE
	'--------------------------------
	'AUFRUF DER VERSCHIEDENEN START -
	'STRATEGIEN WELD-STRICHRAUPEN
	 IFTHENEXP STARTWAHL=-3
		'STRATEGIE 1
		 CALL JOB:WELD_MAEANDER_INKREMENTAL ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=-2
		'STRATEGIE 2
		 CALL JOB:WELD_MAEANDER_INKREMENTAL ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=-1
		'STRATEGIE 3
		 CALL JOB:WELD_MAEANDER_INKREMENTAL ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ELSEIFEXP STARTWAHL=0
		'STRATEGIE 4
		 CALL JOB:WELD_MAEANDER_INKREMENTAL ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFRAUPENANZAHL_S
	 ENDIF
	'HOEHEN ERNEUT VERMESSEN
	'H1 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
	 GETS H1_LAGE_IST $RV
	'SET H1_LAGE_IST 1500
	 ADD H1_GESAMT_IST H1_LAGE_IST
	'H2 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
	 GETS H2_LAGE_IST $RV
	'SET H2_LAGE_IST 3300
	 ADD H2_GESAMT_IST H2_LAGE_IST
	'H3 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H3 ARGF0
	 GETS H3_LAGE_IST $RV
	'SET H3_LAGE_IST 3400
	 ADD H3_GESAMT_IST H3_LAGE_IST
	'H4 MESSEN
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H4 ARGF0
	 GETS H4_LAGE_IST $RV
	'SET H4_LAGE_IST 1400
	 ADD H4_GESAMT_IST H4_LAGE_IST
	'MINIMALE HOEHE VON H1 BIS H4
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'GEM. HOEHEN IN PUNKTE SCHREIBEN
	 SETE Pkt_1 (3) H1_GESAMT_IST
	 SETE Pkt_2 (3) H1_GESAMT_IST
	 SETE Pkt_3 (3) H4_GESAMT_IST
	 SETE Pkt_4 (3) H4_GESAMT_IST
	 SETE GET_H1 (3) H1_GESAMT_IST
	 SETE GET_H2 (3) H2_GESAMT_IST
	 SETE GET_H3 (3) H3_GESAMT_IST
	 SETE GET_H4 (3) H4_GESAMT_IST
	'SCANNEN ---> LAGE
	 CALL JOB:SCAN_LINEAR_ARG_P ARGFGET_H1 ARGFGET_H4 ARGF15000 ARGFV_SCAN
	 TIMER T=5.00
	'--------------------------------
	'Raupe erhöhen nach Mäander
	'CALL JOB:INC_NR ARGF "RAUPE_NR"
	'--------------------------------
	'AUFRUF DER VERSCHIEDENEN START -
	'STRATEGIEN RAISE-FALLING-EDGE
	 IFTHENEXP STARTWAHL=0
		'STRATEGIE 1
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_2 (3) H1_GESAMT_IST
		 ENDIF
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-1
		'STRATEGIE 2
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_1 (3) H1_GESAMT_IST
		 ENDIF
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-2
		'STRATEGIE 3
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_3 ARGFPkt_4 ARGFPkt_1 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_1 ARGFPkt_2 ARGFPkt_3 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_2 (3) H1_GESAMT_IST
		 ENDIF
	 ELSEIFEXP STARTWAHL=-3
		'STRATEGIE 4
		 IFTHENEXP H4_GESAMT_IST<H3_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_4 ARGFPkt_3 ARGFPkt_2 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H4_GESAMT_IST $RV
			 SETE Pkt_3 (3) H4_GESAMT_IST
			 SETE Pkt_4 (3) H4_GESAMT_IST
		 ENDIF
		 IFTHENEXP H1_GESAMT_IST<H2_GESAMT_IST ANDEXP LAGE_IST>1
			 IFTHENEXP RFE_DIR="OUTWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_OUTWARDS ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ELSEIFEXP RFE_DIR="INWARDS"
				 CALL JOB:RFE_STRICHRAUPEN_INWARDS ARGFPkt_2 ARGFPkt_1 ARGFPkt_4 ARGFVERSATZ ARGFKENNLINIE ARGFV_WELD ARGFUSERFRAME ARGFOFFSET_Y
			 ENDIF
			 GETS H1_GESAMT_IST $RV
			 SETE Pkt_1 (3) H1_GESAMT_IST
			 SETE Pkt_1 (3) H1_GESAMT_IST
		 ENDIF
	 ENDIF
	 TIMER T=1.00
	 CALL JOB:MIN_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
	 GETS H_MIN $RV
	'--------------------------------
	'ZWISCHENLAGENZEIT
	 CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
	 TIMER T=10.00
	'--------------------------------
	'LAGE WIRD IN TC UM 1 ERHOEHT
	' --> R=1, S=0
	 CALL JOB:INC_NR ARGF"LAGE_NR"
ENDWHILE
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
