/JOB
//NAME BLOCK_SCAN
///FOLDERNAME SCAN_JOBS
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 1,0,19,0,0,11,0,0
LB000 SCAN_GIEBEL
LD000 BLOCKLAENGE
LD001 BLOCKHOEHE
LD002 BLOCKBREITE
LD003 SCANZUGABE
LD004 V_MOVE
LD005 V_SCAN
LD006 OFFSET_X
LD007 OFFSET_Y
LD008 H_IST
LD009 H_ZUSATZ
LD010 USERFRAME
LD011 SCANROTATION_Y
LD012 OFFSET_Z
LD013 X_1
LD014 X_2
LD015 Y_1
LD016 Y_2
LD019 SCANROTATION_X
LD020 REST_Z
LP001 SCAN_1
LP002 SCAN_2
LP003 SCAN_3
LP004 SCAN_4
LP005 RESTPOINT
LP006 SCAN_5
LP007 SCAN_6
LP008 SCAN_7
LP009 SCAN_8
LP010 SCAN_9
LP011 SCAN_10
//INST
///DATE 2023/07/21 10:42
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,0,21,0,0,12,0,0
NOP
CALL JOB:TRIGGER_RESET
'CALL JOB:USERFRAME_KALIB
'IDs Eingeben
CALL JOB:SET_IDS_FULL ARGF512 ARGF1 ARGF1 ARGF1 ARGF0
'--------------------------------
'OFFSET
SET OFFSET_X 0
SET OFFSET_Y 50000
SET OFFSET_Z -25000
'--------------------------------
'DIE GIEBEL DES BLOCKES SCANNEN?
SET SCAN_GIEBEL 1
'--------------------------------
'GEOMETRIE
SET BLOCKLAENGE 400000
SET BLOCKBREITE -50000
SET BLOCKHOEHE 185000
SET USERFRAME 4
SET SCANZUGABE 15000
SET SCANROTATION_X 400000
SET SCANROTATION_Y 300000
SET H_ZUSATZ 45000
SET H_IST OFFSET_Z
SET REST_Z  EXPRESS BLOCKHOEHE + 100000
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF100 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF40 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'USERFRAME in TwinCAT setzen
CALL JOB:SET_UFRAME ARGFUSERFRAME
'--------------------------------
'Punktkoordinaten Berechnen
SET X_1  EXPRESS OFFSET_X - BLOCKLAENGE / 2 - SCANZUGABE
SET X_2  EXPRESS OFFSET_X + BLOCKLAENGE / 2 + SCANZUGABE
SET Y_1  EXPRESS OFFSET_Y - BLOCKBREITE / 2 - SCANZUGABE
SET Y_2  EXPRESS OFFSET_Y + BLOCKBREITE / 2 + SCANZUGABE
'Scanpunkte
'Seiten
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_1 ARGFOFFSET_Z
SET SCAN_1 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_1 ARGFOFFSET_Z
SET SCAN_2 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFY_2 ARGFOFFSET_Z
SET SCAN_3 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFY_2 ARGFOFFSET_Z
SET SCAN_4 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFOFFSET_X ARGFOFFSET_Y ARGFREST_Z
SET RESTPOINT P100
'Giebel
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFOFFSET_Z
SET SCAN_5 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFBLOCKHOEHE
SET SCAN_6 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFOFFSET_Z
SET SCAN_7 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFBLOCKHOEHE
SET SCAN_8 P100
'x-Rotationen einfügen seite
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_1 ARGF"Rx" ARGFSCANROTATION_X
SET SCAN_1 P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_2 ARGF"Rx" ARGFSCANROTATION_X
SET SCAN_2 P100
MUL SCANROTATION_X -1
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_3 ARGF"Rx" ARGFSCANROTATION_X
SET SCAN_3 P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_4 ARGF"Rx" ARGFSCANROTATION_X
SET SCAN_4 P100
'y-Rotationen einfügen seite
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_5 ARGF"Ry" ARGFSCANROTATION_Y
SET SCAN_5 P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_6 ARGF"Ry" ARGFSCANROTATION_Y
SET SCAN_6 P100
MUL SCANROTATION_Y -1
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_7 ARGF"Ry" ARGFSCANROTATION_Y
SET SCAN_7 P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_8 ARGF"Ry" ARGFSCANROTATION_Y
SET SCAN_8 P100
'Giebelscanhöhe mit Scanzugabe
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_5 ARGF"z" ARGFSCANZUGABE
SET SCAN_5 P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFSCAN_7 ARGF"z" ARGFSCANZUGABE
SET SCAN_7 P100
'Decklage
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_1 ARGFOFFSET_Y ARGFBLOCKHOEHE
SET SCAN_9 P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_2 ARGFOFFSET_Y ARGFBLOCKHOEHE
SET SCAN_10 P100
'Scannen
CALL JOB:SET_TCPON ARGF1
TCPON TL#(1)
MOVL SCAN_2 V=V_MOVE
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EXT"
TIMER T=0.50
'Erste Seite
WHILEEXP H_IST<BLOCKHOEHE
	 MOVL SCAN_2 V=V_MOVE
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 MOVL SCAN_1 V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
	 ADD H_IST H_ZUSATZ
	'Höhe aktualisieren
	 SETE SCAN_1 (3) H_IST
	 SETE SCAN_2 (3) H_IST
	 TIMER T=0.50
ENDWHILE
'SCAN ERSTER GIEBEL
IFTHENEXP SCAN_GIEBEL=1
	 MOVL SCAN_7 V=V_MOVE
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 MOVL SCAN_8 V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
ENDIF
'RESTPOINT ANFAHREN
MOVL RESTPOINT V=V_MOVE
'zweite Seite
SET H_IST OFFSET_Z
WHILEEXP H_IST<BLOCKHOEHE
	 MOVL SCAN_3 V=V_MOVE
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 MOVL SCAN_4 V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
	 ADD H_IST H_ZUSATZ
	'Höhe aktualisieren
	 SETE SCAN_3 (3) H_IST
	 SETE SCAN_4 (3) H_IST
	 TIMER T=0.50
ENDWHILE
'SCAN ZWEITER GIEBEL
IFTHENEXP SCAN_GIEBEL=1
	 MOVL SCAN_5 V=V_MOVE
	 CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
	 MOVL SCAN_6 V=V_SCAN
	 CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
ENDIF
'Decklage
MOVL SCAN_9 V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
MOVL SCAN_10 V=V_SCAN
CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
TCPOF
MOVL RESTPOINT V=V_MOVE
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
