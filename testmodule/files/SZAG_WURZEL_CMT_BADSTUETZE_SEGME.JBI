/JOB
//NAME SZAG_WURZEL_CMT_BADSTUETZE_SEGME
///FOLDERNAME SZAG
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,2,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
///LVARS 0,4,15,0,0,4,0,0
LI000 LAGE_NR
LI001 SCHW_KENNLINIE
LI002 SEGMENT_IST
LI003 SEGMENT_SOLL
LD000 V_SCAN
LD001 V_MOVE
LD002 SCANZUGABE
LD003 X_OFFSET
LD004 Y_OFFSET
LD005 Z_OFFSET
LD006 USERFRAME
LD007 NAHTLAENGE
LD008 SEGMENT_LENGTH
LD009 X_ROTATION
LD010 WERKZEUG
LD011 Z_ROTATION
LD012 Y_ROTATION
LD013 MESSEINZUG
LD015 X_OFFSET_BAD
LP000 WELDSTART_FULL
LP001 WELDSTART_BAD
LP002 RESTPOINT
LP003 WELDEND
//INST
///DATE 2022/03/17 17:01
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,4,20,0,0,8,0,0
NOP
CALL JOB:TRIGGER_RESET
'-----
'IDs Eingeben
CALL JOB:SET_ID ARGF479 ARGF"WID"
CALL JOB:SET_ID ARGF1 ARGF"NAHT_NR"
CALL JOB:SET_ID ARGF1 ARGF"LAGE_NR"
CALL JOB:SET_ID ARGF2 ARGF"RAUPE_NR"
CALL JOB:SET_ID ARGF0 ARGF"SCHW_NR"
SET X_OFFSET 370000
SET X_OFFSET_BAD 208818
SET Y_OFFSET 27875
SET Z_OFFSET -62057
SET USERFRAME 10
SET SCANZUGABE 25000
SET MESSEINZUG 40000
SET NAHTLAENGE -290000
SET SCHW_KENNLINIE 203
SET SEGMENT_SOLL 10
SET SEGMENT_IST 1
SET X_ROTATION 0
SET Y_ROTATION 40000
SET Z_ROTATION 0
SET WERKZEUG 7
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF150 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'BERECHNUNGEN
SET H1_GESAMT_IST Z_OFFSET
SET H2_GESAMT_IST Z_OFFSET
SET SEGMENT_LENGTH  EXPRESS NAHTLAENGE / ( SEGMENT_SOLL )
IFTHENEXP NAHTLAENGE<0
	 MUL MESSEINZUG -1
ENDIF
'Startpunkt für Berechnung der
'Mess-und Scanpunkte
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET ARGFY_OFFSET ARGFH1_GESAMT_IST
SET WELDSTART_FULL P100
'Startpunktrotation
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART_FULL ARGF"RZ" ARGFZ_ROTATION
SET WELDSTART_FULL P100
'Startpunkt für Badstütze
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET_BAD ARGFY_OFFSET ARGFH1_GESAMT_IST
SET WELDSTART_BAD P100
'Start Badstütze rotation
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART_BAD ARGF"RY" ARGFY_ROTATION
SET WELDSTART_BAD P100
'Endpunkt
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART_FULL ARGF"x" ARGFNAHTLAENGE
SET WELDEND P100
SETE WELDEND (3) H2_GESAMT_IST
'RESTPOINT
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF0 ARGF0 ARGF300000
SET RESTPOINT P100
' AKTUELLE LAGE SPEICHERN
CALL JOB:GET_ID ARGF"LAGE_NR"
GETS LAGE_NR $RV
'--------------------------------
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
COMM CALL JOB:TRIGGER ARGF"UI_START"
CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'------- SCAN LAGE 0 ------------
CALL JOB:SCAN_LINEAR_ARG_P ARGFWELDSTART_FULL ARGFWELDEND ARGFSCANZUGABE ARGFV_SCAN
COMM CALL JOB:TRIGGER ARGF"UI_STOP"
COMM TIMER T=10.00
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART_FULL ARGF"RY" ARGFY_ROTATION
SET WELDSTART_FULL P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDEND ARGF"RY" ARGFY_ROTATION
SET WELDEND P100
'--------------------------------
'Badstützenstartpunkt
' um y rotieren
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART_BAD ARGF"RX" ARGFX_ROTATION
SET WELDSTART_BAD P100
'Endpunktrotation auf null setzen
SETE WELDEND (6) 0
'--- SCHWEISSEN WURZELLAGE ------
CALL JOB:SET_TCPON ARGFWERKZEUG
TCPON TL#(WERKZEUG)
CALL JOB:SET_ID ARGFLAGE_NR ARGF"LAGE_NR"
'WELDSTART ANPASSEN
MOVL WELDSTART_BAD V=V_MOVE
CALL JOB:TRIGGER ARGF"UI_START"
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
DOUT OT#(60) ON
ARCON ASF#(SCHW_KENNLINIE)
MOVL WELDEND V=5.0
ARCOF AEF#(SCHW_KENNLINIE)
DOUT OT#(60) OFF
CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
TCPOF
CALL JOB:TRIGGER ARGF"UI_STOP"
'TIMER T=20.00
SETE WELDSTART_FULL (5) 0
SETE WELDEND (5) 0
SETE WELDEND (6) Z_ROTATION
'--------------------------------
'Startpunktrotation
'MUL X_ROTATION -1
'------- SCAN WURZELLAGE --------
'Endpunktrotation auf null setzen
CALL JOB:SCAN_LINEAR_ARG_P ARGFWELDSTART_FULL ARGFWELDEND ARGFSCANZUGABE ARGFV_SCAN
CALL JOB:SET_TCPON ARGFWERKZEUG
TCPON TL#(WERKZEUG)
MUL Z_ROTATION -1
SETE RESTPOINT (6) Z_ROTATION
IMOV RESTPOINT V=V_MOVE UF#(USERFRAME)
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
'TIMER T=20.00
TCPOF
CALL JOB:TRIGGER_RESET
END
