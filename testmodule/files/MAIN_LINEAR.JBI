/JOB
//NAME MAIN_LINEAR
///FOLDERNAME STANDARD
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,4,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D010 H1_LAGE_IST
D011 H2_LAGE_IST
///LVARS 0,2,15,0,0,6,0,0
LI000 LAGE_NR
LI001 SCHW_KENNLINIE
LD001 V_SCAN
LD002 V_MOVE
LD003 SCANZUGABE
LD004 X_OFFSET
LD005 Y_OFFSET
LD006 Z_OFFSET
LD007 X_ROTATION
LD008 Z_ROTATION
LD009 Y_ROTATION
LD010 USERFRAME
LD011 NAHTLAENGE
LD012 WERKZEUG
LD013 MESSEINZUG
LD014 H_DIFF
LD015 V_WELD
LP000 WELDSTART
LP001 WELD_INKR
LP002 RESTPOINT
LP003 WELDEND
LP004 GET_H1
LP005 GET_H2
//INST
///DATE 2023/07/17 07:19
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,4,16,0,0,6,0,0
NOP
'--------------------------------
'MAIN_LINEAR
'Höhenmessung der Schweißpunkte
'Scan der Substratplatte
'Schweißen einer linearen Raupe
'  Rx, Ry, Rz möglich
'Scan der Raupe
'--------------------------------
CALL JOB:TRIGGER_RESET
'-----
'ID'S SETZEN
CALL JOB:SET_IDS_FULL ARGF511 ARGF4 ARGF1 ARGF1 ARGF0
'--------------------------------
SET X_OFFSET 110000
SET Y_OFFSET -110000
SET Z_OFFSET 35000
SET X_ROTATION 0
SET Y_ROTATION 0
SET Z_ROTATION 0
SET USERFRAME 4
SET SCANZUGABE 10000
SET MESSEINZUG 5000
SET NAHTLAENGE 40000
SET SCHW_KENNLINIE 74
SET WERKZEUG 0
'--------------------------------
'WELD-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF57 ARGF"cm/min"
GETS V_WELD $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF20 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF150 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'BERECHNUNGEN
SET H1_GESAMT_IST Z_OFFSET
SET H2_GESAMT_IST Z_OFFSET
IFTHENEXP NAHTLAENGE<0
	 MUL MESSEINZUG -1
ENDIF
'Startpunkt
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET ARGFY_OFFSET ARGFH1_GESAMT_IST
SET WELDSTART P100
'Startpunktrotation
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART ARGF"RZ" ARGFZ_ROTATION
SET WELDSTART P100
'Endpunkt
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART ARGF"x" ARGFNAHTLAENGE
SET WELDEND P100
SETE WELDEND (3) H2_GESAMT_IST
'GET_H1
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART ARGF"x" ARGFMESSEINZUG
SET GET_H1 P100
'GET_H1
MUL MESSEINZUG -1
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDEND ARGF"x" ARGFMESSEINZUG
SET GET_H2 P100
'RESTPOINT
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGF0 ARGF0 ARGF300000
SET RESTPOINT P100
' AKTUELLE LAGE SPEICHERN
CALL JOB:GET_ID ARGF"LAGE_NR"
GETS LAGE_NR $RV
'--------------------------------
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
COMM CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'Höhen Messen
'H1 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H1 ARGF0
GETS H1_LAGE_IST $RV
ADD H1_GESAMT_IST H1_LAGE_IST
'H2 Messen
CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_H2 ARGF0
GETS H2_LAGE_IST $RV
ADD H2_GESAMT_IST H2_LAGE_IST
'Höhen Schreiben
SETE WELDSTART (3) H1_GESAMT_IST
SETE WELDEND (3) H2_GESAMT_IST
SETE GET_H1 (3) H1_GESAMT_IST
SETE GET_H2 (3) H2_GESAMT_IST
'Höhendifferenz messen
SET H_DIFF  EXPRESS H2_GESAMT_IST - H1_GESAMT_IST
'Inkrementelle Schweißbew.
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFNAHTLAENGE ARGF0 ARGFH_DIFF
SET WELD_INKR P100
'------- SCAN SUBSTRAT -------
CALL JOB:SCAN_LINEAR_ARG_P ARGFWELDSTART ARGFWELDEND ARGFSCANZUGABE ARGFV_SCAN
'Rotationen auf Schweißpunkte
'addieren
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART ARGF"RX" ARGFX_ROTATION
SET WELDSTART P100
CALL JOB:SHIFT_PKT_ELEMENT ARGFWELDSTART ARGF"RY" ARGFY_ROTATION
SET WELDSTART P100
'--- SCHWEISSEN
CALL JOB:SET_TCPON ARGFWERKZEUG
TCPON TL#(WERKZEUG)
CALL JOB:SET_ID ARGFLAGE_NR ARGF"LAGE_NR"
MOVL WELDSTART V=V_MOVE
CALL JOB:TRIGGER ARGF"UI_START"
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
CALL JOB:XIRIS ARGF2 ARGF"VIDEO_EIN"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
CALL JOB:XIRIS ARGF2 ARGF"TRIG_EIN"
CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
DOUT OT#(60) ON
ARCON ASF#(SCHW_KENNLINIE)
COMM WVON AMP=4.8 FREQ=1.0 ANGL=0.0
IMOV WELD_INKR V=V_WELD UF#(USERFRAME)
COMM WVOF
ARCOF AEF#(SCHW_KENNLINIE)
DOUT OT#(60) OFF
CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
CALL JOB:XIRIS ARGF2 ARGF"TRIG_AUS"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
CALL JOB:XIRIS ARGF2 ARGF"VIDEO_AUS"
TCPOF
CALL JOB:TRIGGER ARGF"UI_STOP"
'TIMER T=20.00
SETE WELDSTART (4) 0
SETE WELDSTART (5) 0
'------- SCAN RAUPE --------
CALL JOB:SCAN_LINEAR_ARG_P ARGFWELDSTART ARGFWELDEND ARGFSCANZUGABE ARGFV_SCAN
CALL JOB:SET_TCPON ARGFWERKZEUG
TCPON TL#(WERKZEUG)
IMOV RESTPOINT V=V_MOVE UF#(USERFRAME)
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
'TIMER T=20.00
TCPOF
CALL JOB:TRIGGER_RESET
END
