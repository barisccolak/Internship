/JOB
//NAME RAISE_FALLING_EDGE_STRICHRAUPEN
///FOLDERNAME STRICHRAUPEN
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///GVARS 0,0,4,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D002 H3_GESAMT_IST
D003 H4_GESAMT_IST
///LVARS 0,3,16,0,0,9,0,0
LI000 SPUR_SOLL
LI001 SPUR_IST
LI002 WELD_TOOL
LD000 OFFSET_Y
LD001 H_AUSGLEICHSLAGE
LD002 KENNLINIE
LD003 V_WELD
LD004 USERFRAME
LD005 V_MOVE
LD006 OFFSET_1
LD007 OFFSET_2
LD008 VERSATZ
LD009 H_LOW
LD010 H_HIGH
LD011 AUSGLEICHSSPUREN
LD012 V_SCAN
LD013 SCANZUGABE
LD014 STICHWINKEL
LD015 STICHDISTANZ
LP000 PKT_1
LP001 PKT_2
LP002 PKT_3
LP003 WELD_INKR
LP004 MOVE_INKR
LP005 OFFSET_INKR
LP006 GET_HEIGHT
LP007 WELD_START
LP008 WELD_END
//ARGINFO
///ARGTYPE P,P,P,D,D,D,D,D
///COMMENT
ECKPUNKT_1
ECKPUNKT_2
ECKPUNKT_3
VERSATZ
KENNLINIE
V_WELD
USERFRAME
OFFSET_Y
//INST
///DATE 2021/10/11 10:17
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,3,16,0,0,9,0,0
NOP
'--------------------------------
'Argumente einlesen
GETARG PKT_1 IARG#(1)
GETARG PKT_2 IARG#(2)
GETARG PKT_3 IARG#(3)
GETARG VERSATZ IARG#(4)
GETARG KENNLINIE IARG#(5)
GETARG V_WELD IARG#(6)
GETARG USERFRAME IARG#(7)
GETARG OFFSET_Y IARG#(8)
'--------------------------------
' WINKEL FUER STECHEN SCHLEPPEN
SET STICHWINKEL 0
'--------------------------------
' DISTANZ FUER STECHEN SCHLEPPEN
SET STICHDISTANZ 0
'--------------------------------
'--------------------------------
'PUNKT FUER ERNEUTE HOEHEN
'MESSUNG
SET GET_HEIGHT PKT_1
'Rotation z auf null setzen
SETE GET_HEIGHT (6) 0
SETE GET_HEIGHT (2) OFFSET_Y
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
' SCANZUGABE plus/minus setzen
SET SCANZUGABE 10000
'--------------------------------
' SCHWEISS TOOL auswaehlen
SET WELD_TOOL 0
'--------------------------------
'Inkrementalpunkte Berechnen
SET WELD_INKR PKT_2
SET MOVE_INKR PKT_1
SET OFFSET_INKR PKT_3
SETE OFFSET_INKR (3) 0
SUB WELD_INKR PKT_1
SUB MOVE_INKR PKT_2
SUB OFFSET_INKR PKT_1
SETE OFFSET_INKR (3) 0
GETE OFFSET_1 OFFSET_INKR (1)
GETE OFFSET_2 OFFSET_INKR (2)
'--------------------------------
'PUNKHOEHEN ZUWEISEN
GETE H_LOW PKT_1 (3)
IFTHENEXP H_LOW=H1_GESAMT_IST
	 SET H_HIGH H2_GESAMT_IST
ELSEIFEXP H_LOW=H4_GESAMT_IST
	 SET H_HIGH H3_GESAMT_IST
ENDIF
IFTHENEXP OFFSET_1<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_1>0
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_2<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (2) VERSATZ
ELSEIFEXP OFFSET_2>0
	 SETE OFFSET_INKR (2) VERSATZ
ENDIF
'--------------------------------
'Minimale Ausgleichsspuranzahl
SET AUSGLEICHSSPUREN 1
WHILEEXP H_LOW<H_HIGH
	 SET WELD_START PKT_1
	 SET WELD_END PKT_2
	 SET SPUR_IST 1
	 SET SPUR_SOLL  EXPRESS SPUR_IST + AUSGLEICHSSPUREN
	 WHILEEXP SPUR_IST<SPUR_SOLL
		'--------------------------------
		'SCHWEISSEN
		 CALL JOB:WELD_LINEAR ARGFWELD_START ARGFWELD_END ARGFV_WELD ARGF80 ARGFWELD_TOOL ARGFV_MOVE ARGFSTICHWINKEL ARGFSTICHDISTANZ
		'--------------------------------
		'Aufruf des Servicepositionsjobs
		'Roboter fährt aus akktueller
		'Position 150mm nach oben falls
		'Register 312 auf 1 gesetzt wird
		 CALL JOB:SERVICEPAUSE ARGFUSERFRAME ARGFWELD_TOOL
		'--------------------------------
		'SCANNEN
		 CALL JOB:SCAN_LINEAR_ARG_P ARGFWELD_END ARGFWELD_START ARGFSCANZUGABE ARGFV_SCAN
		'--------------------------------
		'ZWISCHENRAUPENZEIT
		 TIMER T=5.00
		 ADD SPUR_IST 1
		 ADD WELD_START OFFSET_INKR
		 ADD WELD_END OFFSET_INKR
		'--------------------------------
		'Raupe erhöhen nach schweissung
		 CALL JOB:INC_NR ARGF"RAUPE_NR"
	 ENDWHILE
	'--------------------------------
	'Neue Kantenhoehe erfassen
	 CALL JOB:GET_LAYER_HEIGHT_ARG_P ARGFGET_HEIGHT ARGF0
	 GETS H_AUSGLEICHSLAGE $RV
	 ADD H_LOW H_AUSGLEICHSLAGE
	 SETE PKT_1 (3) H_LOW
	 SETE PKT_2 (3) H_LOW
	 SETE GET_HEIGHT (3) H_LOW
	 ADD AUSGLEICHSSPUREN 1
ENDWHILE
TCPOF
RET H_LOW
END
