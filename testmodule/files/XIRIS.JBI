/JOB
//NAME XIRIS
///FOLDERNAME TRIGGER_JOBS
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 1,3,0,0,1,0,0,0
LB000 B_IST
LI000 CAMERA
LI001 I_IST
LI002 I_SOLL
LS000 BEFEHL
//ARGINFO
///ARGTYPE I,S,,,,,,
///COMMENT
CAMERA
BEFEHL






//INST
///DATE 2021/03/16 10:53
///ATTR SC,RW
///LVARS 2,3,0,0,2,0,0,0
NOP
'ARGUMENTE EINLESEN
GETARG CAMERA IARG#(1)
GETARG BEFEHL IARG#(2)
'Status lokal auslesen
IFTHENEXP CAMERA=1
	 GETREG I_IST MREG#(64) LOW
ELSEIFEXP CAMERA=2
	 GETREG I_IST MREG#(64) HIGH
ELSE
	 MSG "FALSCHE EINGABE XIRIS"
	 SET B000  EXPRESS -500000
ENDIF
'Status in BYTE konvertieren
SET B_IST I_IST
'Befehls-Bit setzen
IFTHENEXP BEFEHL="VIDEO_EIN"
	 OR B_IST 1
ELSEIFEXP BEFEHL="VIDEO_AUS"
	 AND B_IST 254
ELSEIFEXP BEFEHL="IMAGE_EIN"
	 OR B_IST 2
ELSEIFEXP BEFEHL="IMAGE_AUS"
	 AND B_IST 253
ELSEIFEXP BEFEHL="TRIG_EIN"
	 OR B_IST 4
ELSEIFEXP BEFEHL="TRIG_AUS"
	 AND B_IST 251
ELSE
	 MSG "FALSCHE EINGABE XIRIS"
	 SET B000  EXPRESS -500000
ENDIF
'Status in INTEGER konvertieren
SET I_SOLL B_IST
'Status in Register schreiben
IFTHENEXP CAMERA=1
	 SETREG MREG#(64) LOW I_SOLL
ELSEIFEXP CAMERA=2
	 SETREG MREG#(64) HIGH I_SOLL
ENDIF
END
