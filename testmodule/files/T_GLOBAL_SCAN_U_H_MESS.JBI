/JOB
//NAME T_GLOBAL_SCAN_U_H_MESS
///FOLDERNAME TWINCAT_KOMMUNIKATION
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///GVARS 0,0,8,0,0,0,0,0
D000 H1_GESAMT_IST
D001 H2_GESAMT_IST
D002 H3_GESAMT_IST
D003 H4_GESAMT_IST
D004 H5_GESAMT_IST
D005 H6_GESAMT_IST
D006 H7_GESAMT_IST
D007 H8_GESAMT_IST
///LVARS 0,2,19,0,0,6,0,0
LI000 USERFRAME
LI001 LAGE_NR_IST
LD000 H_START
LD001 H_MAX
LD002 SCAN_START_X
LD003 SCAN_ENDE_X
LD004 X_ZENTRUM
LD005 X_H1
LD006 X_H2
LD007 Y_H1_H2
LD008 Y_ZENTRUM
LD009 Y_H3
LD010 Y_H4
LD011 V_SCAN
LD012 BREITE_GESAMT
LD013 TIEFE_GESAMT
LD014 TIEFE_STEG
LD015 BREITE_STEG
LD016 SPURBREITE
LD017 V_MOVE
LD018 Z_ROT
LP000 PKT_GET_H1
LP001 PKT_GET_H2
LP002 PKT_GET_H3
LP003 PKT_GET_H4
LP004 PKT_SCAN_START
LP005 PKT_SCAN_ENDE
//ARGINFO
///ARGTYPE D,D,D,D,D,I,D,
///COMMENT
BREITE_GESAMT
TIEFE_GESAMT
TIEFE_STEG
BREITE_STEG
SPURBREITE
USERFRAME
H_START

//INST
///DATE 2021/01/20 14:45
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,2,19,0,0,6,0,0
NOP
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF60 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF60 ARGF"mm/s"
GETS V_MOVE $RV
'
'################################
'--------------------------------
GETARG BREITE_GESAMT IARG#(1)
GETARG TIEFE_GESAMT IARG#(2)
GETARG TIEFE_STEG IARG#(3)
GETARG BREITE_STEG IARG#(4)
GETARG SPURBREITE IARG#(5)
GETARG USERFRAME IARG#(6)
GETARG H_START IARG#(7)
'
'################################
'OFFSET aus P100 übernehmen
GETE X_ZENTRUM P100 (1)
GETE Y_ZENTRUM P100 (2)
'
'################################
'Lage NR aus TC einlesen
CALL JOB:GET_ID ARGF"LAGE_NR"
GETS LAGE_NR_IST $RV
'
'MAXIMALHOEHE FUER SCAN BESTIMMEN
'################################
CALL JOB:MAX_BACK ARGFH1_GESAMT_IST ARGFH2_GESAMT_IST ARGFH3_GESAMT_IST ARGFH4_GESAMT_IST
GETS H_MAX $RV
'
'################################
'Berechnung von Punktkoordinaten
SET SCAN_START_X  EXPRESS X_ZENTRUM - BREITE_GESAMT / 2 - 10000
SET SCAN_ENDE_X  EXPRESS X_ZENTRUM + BREITE_GESAMT / 2 + 10000
SET X_H1  EXPRESS X_ZENTRUM - BREITE_GESAMT / 2 + 10000
SET X_H2  EXPRESS X_ZENTRUM + BREITE_GESAMT / 2 - 10000
SET Y_H1_H2  EXPRESS Y_ZENTRUM + TIEFE_GESAMT / 2 - TIEFE_STEG / 2
SET Y_H3  EXPRESS Y_ZENTRUM + TIEFE_GESAMT / 2 - TIEFE_STEG - 10000
SET Y_H4  EXPRESS Y_ZENTRUM - TIEFE_GESAMT / 2 + 10000
'
'################################
'PUNKTE ERZEUGEN
'SCAN_START
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFSCAN_START_X ARGFY_ZENTRUM ARGFH_MAX
SET PKT_SCAN_START P100
'--------------------------------
'SCAN_ENDE
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFSCAN_ENDE_X ARGFY_ZENTRUM ARGFH_MAX
SET PKT_SCAN_ENDE P100
'--------------------------------
'GET_H1
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFX_H1 ARGFY_H1_H2 ARGFH1_GESAMT_IST
SET PKT_GET_H1 P100
'--------------------------------
'GET_H2
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFX_H2 ARGFY_H1_H2 ARGFH2_GESAMT_IST
SET PKT_GET_H2 P100
'--------------------------------
'GET_H3
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFX_ZENTRUM ARGFY_H1_H2 ARGFH3_GESAMT_IST
SET PKT_GET_H3 P100
'--------------------------------
'GET_H4
CALL JOB:PUNKTE_ERZEUGEN_UF ARGF4 ARGFX_ZENTRUM ARGFY_H4 ARGFH4_GESAMT_IST
SET PKT_GET_H4 P100
GETE Z_ROT PKT_GET_H4 (6)
SUB Z_ROT 900000
SETE PKT_GET_H4 (6) Z_ROT
'--------------------------------
'
'################################
'             SCAN
TCPON TL#(1)
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
MOVL PKT_SCAN_START V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EXT"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_EIN"
DOUT OT#(1) ON
MOVL PKT_SCAN_ENDE V=V_SCAN
DOUT OT#(1) OFF
CALL JOB:LLT ARGF1 ARGF"TRIG_AUS"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"TRIG_INT"
TIMER T=0.50
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TIMER T=1.00
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
TCPOF
'
'################################
'-----  LAGENHOEHE MESSEN  -----'
'
'################################
'     MESSUNG H1_LAGE_IST
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPON TL#(1)
MOVL PKT_GET_H1 V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.20
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS H5_GESAMT_IST $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
'
'################################
'     MESSUNG H3_LAGE_IST
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPON TL#(1)
MOVL PKT_GET_H3 V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.20
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS H7_GESAMT_IST $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
'
'################################
'     MESSUNG H2_LAGE_IST
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPON TL#(1)
MOVL PKT_GET_H2 V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.20
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS H6_GESAMT_IST $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
'
'################################
'     MESSUNG H4_LAGE_IST
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPON TL#(1)
MOVL PKT_GET_H4 V=V_MOVE
CALL JOB:LLT ARGF1 ARGF"LASER_EIN"
TIMER T=0.20
DOUT OT#(2) ON
TIMER T=0.01
DOUT OT#(2) OFF
CALL JOB:IMPORT_DOUBLE_FROM_TC ARGF310 ARGF311
GETS H8_GESAMT_IST $RV
TIMER T=0.20
CALL JOB:LLT ARGF1 ARGF"LASER_AUS"
TCPOF
'
'################################
'GEMESSENE LAGENHOEHE AUF
'T_Hx_IST WERTE ADDIEREN
ADD H1_GESAMT_IST H5_GESAMT_IST
ADD H2_GESAMT_IST H6_GESAMT_IST
ADD H3_GESAMT_IST H7_GESAMT_IST
ADD H4_GESAMT_IST H8_GESAMT_IST
END
