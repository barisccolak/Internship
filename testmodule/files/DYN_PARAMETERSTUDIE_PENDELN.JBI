/JOB
//NAME DYN_PARAMETERSTUDIE_PENDELN
///FOLDERNAME DEMONSTRATOR
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,2,17,1,0,2,0,0
LI000 POINT
LI001 Direction
LD000 BLOCKLAENGE
LD001 SPURANZAHL
LD002 VERSATZ_ZUGABE
LD003 VERSATZ_MIN
LD004 VERSATZ_SOLL
LD005 VERSATZZUGABEN
LD006 V_SCAN
LD007 V_WELD
LD008 BLOCKBREITE
LD009 Z
LD010 KENNLINIE
LD011 USERFRAME
LD012 Y_UPPER
LD013 Y_LOWER
LD014 Y_OFFSET
LD015 X_LEFT
LD017 X_RIGHT
LR000 ABWEICHUNG_SOLL
LP000 SCAN_START
LP001 SCAN_END
//INST
///DATE 2023/02/08 14:48
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,3,20,1,0,2,0,0
NOP
CALL JOB:TRIGGER_RESET
'IDs Eingeben
CALL JOB:SET_ID ARGF497 ARGF"WID"
CALL JOB:SET_ID ARGF9 ARGF"NAHT_NR"
CALL JOB:SET_ID ARGF1 ARGF"LAGE_NR"
CALL JOB:SET_ID ARGF1 ARGF"RAUPE_NR"
'--------------------------------
SET USERFRAME 4
SET Z 0
SET VERSATZ_SOLL 5451
SET ABWEICHUNG_SOLL 516
SET KENNLINIE 5
SET BLOCKBREITE 65000
SET BLOCKLAENGE 65000
SET X_LEFT 230000
SET Y_OFFSET -66000
SET Direction 1
'WELD-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF40 ARGF"cm/min"
GETS V_WELD $RV
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF20 ARGF"mm/s"
GETS V_SCAN $RV
'################################
SET SPURANZAHL  EXPRESS BLOCKLAENGE / ( VERSATZ_SOLL )
SET VERSATZ_MIN  EXPRESS VERSATZ_SOLL - ABWEICHUNG_SOLL
SET VERSATZZUGABEN  EXPRESS SPURANZAHL * ( SPURANZAHL + 1 ) / 2
SET VERSATZ_ZUGABE  EXPRESS ( BLOCKLAENGE - VERSATZ_MIN * SPURANZAHL ) / ( VERSATZZUGABEN )
SET POINT 0
SET Y_UPPER  EXPRESS Y_OFFSET + BLOCKBREITE / 2
SET Y_LOWER  EXPRESS Y_OFFSET - BLOCKBREITE / 2
SET X_RIGHT  EXPRESS X_LEFT + BLOCKLAENGE
'################################
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_LEFT ARGFY_OFFSET ARGFZ
SET SCAN_START P100
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_RIGHT ARGFY_OFFSET ARGFZ
SET SCAN_END P100
WHILEEXP X_LEFT<=X_RIGHT
	 IFTHENEXP POINT>96
		 SET B000  EXPRESS 400
		 MSG "STRING"
	 ENDIF
	 IFTHENEXP Direction=1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_LEFT ARGFY_LOWER ARGFZ
		 SET P[POINT] P100
		 ADD POINT 1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_LEFT ARGFY_UPPER ARGFZ
		 SET P[POINT] P100
		 ADD POINT 1
		 MUL Direction -1
	 ELSEIFEXP Direction=-1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_LEFT ARGFY_UPPER ARGFZ
		 SET P[POINT] P100
		 ADD POINT 1
		 CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_LEFT ARGFY_LOWER ARGFZ
		 SET P[POINT] P100
		 ADD POINT 1
		 MUL Direction -1
	 ENDIF
	 ADD X_LEFT VERSATZ_MIN
	 ADD VERSATZ_MIN VERSATZ_ZUGABE
ENDWHILE
SET D030 POINT
'--------------------------------
'FERTIGUNGSBEGINN
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
'--------------------------------
CALL JOB:USERFRAME_KALIB_ISTROTATION ARGFUSERFRAME
'--------------------------------
'USERFRAME in TwinCAT setzen
CALL JOB:SET_UFRAME ARGFUSERFRAME
'Berechnungen
TCPON TL#(0)
CALL JOB:SET_TCPON ARGF0
'################################
'SCANNING
CALL JOB:SCAN_LINEAR_ARG_P ARGFSCAN_START ARGFSCAN_END ARGF15000 ARGFV_SCAN
'########################
'##########Welding#######
CALL JOB:DEMONSTRATOR_WELD_MEAND ARGFPOINT ARGFV_WELD ARGFKENNLINIE ARGF61
'################################
'SCANNING
CALL JOB:SCAN_LINEAR_ARG_P ARGFSCAN_START ARGFSCAN_END ARGF15000 ARGFV_SCAN
'########################
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
