/JOB
//NAME WELD_MAEANDER
///FOLDERNAME BLOCK_MAEANDER
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 0,5,14,0,0,10,0,0
LI000 SPUR_IST
LI001 SPURRICHTUNG
LI002 WELD_TOOL
LI003 PUNKT_IST
LI004 PUNKT_SOLL
LD000 H_3
LD001 Z_INKRE
LD002 KENNLINIE
LD003 V_WELD
LD004 USERFRAME
LD005 V_MOVE
LD006 OFFSET_1
LD007 OFFSET_2
LD008 VERSATZ
LD009 H_1
LD010 SPUR_SOLL
LD011 SCANZUGABE
LD012 V_SCAN
LD013 V_WELD_VERSATZ
LP000 Pkt_1
LP001 Pkt_2
LP002 Pkt_3
LP003 SPUR_INKR_POS
LP004 SPUR_INKR_NEG
LP005 OFFSET_INKR
LP006 WELD_START
LP007 WELD_END
LP008 SCAN_START
LP009 SCAN_END
//ARGINFO
///ARGTYPE P,P,P,D,D,D,D,I
///COMMENT
ECKPUNKT_1
ECKPUNKT_2
ECKPUNKT_3
VERSATZ
KENNLINIE
V_WELD
USERFRAME
SPUR_SOLL
//INST
///DATE 2022/10/18 16:47
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,5,15,0,0,11,0,0
NOP
'--------------------------------
'ARGUMENTE EINLESEN
GETARG Pkt_1 IARG#(1)
GETARG Pkt_2 IARG#(2)
GETARG Pkt_3 IARG#(3)
GETARG VERSATZ IARG#(4)
GETARG KENNLINIE IARG#(5)
GETARG V_WELD IARG#(6)
GETARG USERFRAME IARG#(7)
GETARG SPUR_SOLL IARG#(8)
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
' V_WELD_VERSATZ
SET V_WELD_VERSATZ  EXPRESS 5 * V_WELD
'--------------------------------
' SCANZUGABE plus/minus setzen
SET SCANZUGABE 10000
'--------------------------------
' SCHWEISS TOOL auswaehlen
SET WELD_TOOL 0
'--------------------------------
SET PUNKT_SOLL  EXPRESS ( SPUR_SOLL + 1 ) * 2
'Inkrementalpunkte Berechnen
SET SPUR_INKR_NEG Pkt_1
SET SPUR_INKR_POS Pkt_2
SET OFFSET_INKR Pkt_3
SUB SPUR_INKR_POS Pkt_1
SUB SPUR_INKR_NEG Pkt_2
SUB OFFSET_INKR Pkt_1
GETE OFFSET_1 OFFSET_INKR (1)
GETE OFFSET_2 OFFSET_INKR (2)
GETE H_1 Pkt_1 (3)
GETE H_3 Pkt_3 (3)
IFTHENEXP OFFSET_1<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_1>0
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_2<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (2) VERSATZ
ELSEIFEXP OFFSET_2>0
	 SETE OFFSET_INKR (2) VERSATZ
ENDIF
SET Z_INKRE  EXPRESS ( H_3 - H_1 ) / ( SPUR_SOLL )
SETE OFFSET_INKR (3) Z_INKRE
CALL JOB:GET_ID ARGF"RAUPE_NR"
GETS SPUR_IST $RV
ADD SPUR_SOLL SPUR_IST
CALL JOB:SET_ID ARGFSPUR_IST ARGF"RAUPE_NR"
SET WELD_START Pkt_1
SET WELD_END Pkt_2
SET SCAN_START Pkt_1
SET SCAN_END Pkt_2
'MAIN STARTEN
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
'--------------------------------
'ZWISCHENRAUPENZEIT
TIMER T=1.00
'--------------------------------
'RAUPE AUS TC EINLESEN
CALL JOB:GET_ID ARGF"RAUPE_NR"
GETS SPUR_IST $RV
'--------------------------------
SET SPURRICHTUNG -1
'ERMOEGLICHT SERVICEPAUSE V. LAGE
CALL JOB:SERVICEPAUSE ARGFUSERFRAME ARGFWELD_TOOL
'TOOL TCP setzen
TCPON TL#(WELD_TOOL)
CALL JOB:SET_TCPON ARGFWELD_TOOL
MOVL Pkt_1 V=V_MOVE
SET PUNKT_IST 1
SET P[PUNKT_IST] Pkt_1
ADD PUNKT_IST 1
ADD Pkt_1 SPUR_INKR_POS
SET P[PUNKT_IST] Pkt_1
WHILEEXP SPUR_IST<=SPUR_SOLL
	 IFTHENEXP SPURRICHTUNG=1
		 ADD PUNKT_IST 1
		 ADD Pkt_1 OFFSET_INKR
		 SET P[PUNKT_IST] Pkt_1
		 ADD PUNKT_IST 1
		 ADD Pkt_1 SPUR_INKR_POS
		 SET P[PUNKT_IST] Pkt_1
	 ELSEIFEXP SPURRICHTUNG=-1
		 ADD PUNKT_IST 1
		 ADD Pkt_1 OFFSET_INKR
		 SET P[PUNKT_IST] Pkt_1
		 ADD PUNKT_IST 1
		 ADD Pkt_1 SPUR_INKR_NEG
		 SET P[PUNKT_IST] Pkt_1
	 ENDIF
	 MUL SPURRICHTUNG -1
	 ADD SPUR_IST 1
ENDWHILE
SET PUNKT_IST 1
'Start TwinCAT and HDR record
CALL JOB:TRIGGER ARGF"UI_START"
TIMER T=0.10
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
TIMER T=1.00
CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
'SCHWEISSEN
MOVL P[PUNKT_IST] V=V_MOVE
DOUT OT#(KENNLINIE) ON
ARCON ASF#(KENNLINIE)
WHILEEXP PUNKT_IST<=PUNKT_SOLL
	 MOVL P[PUNKT_IST] V=V_WELD_VERSATZ
	 ADD PUNKT_IST 1
	 MOVL P[PUNKT_IST] V=V_WELD
	 ADD PUNKT_IST 1
ENDWHILE
ARCOF AEF#(KENNLINIE)
DOUT OT#(KENNLINIE) OFF
'--------------------------------
'Stop TwinCAT and HDR record
CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
CALL JOB:TRIGGER ARGF"UI_STOP"
'--------------------------------
TCPOF
END
