/JOB
//NAME WELD_STRICHRAUPEN
///FOLDERNAME STRICHRAUPEN
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 1,2,15,0,0,11,0,0
LB000 ALTERNIEREN
LI000 SPUR_IST
LI001 WELD_TOOL
LD000 H_3
LD001 Z_INKRE
LD002 KENNLINIE
LD003 V_WELD
LD004 USERFRAME
LD005 V_MOVE
LD006 OFFSET_1
LD007 OFFSET_2
LD008 VERSATZ
LD009 H_1
LD010 SPUR_SOLL
LD011 SCANZUGABE
LD012 V_SCAN
LD013 STICHWINKEL
LD014 STICHDISTANZ
LP000 Pkt_1
LP001 Pkt_2
LP002 Pkt_3
LP003 WELD_INKR
LP004 MOVE_INKR
LP005 OFFSET_INKR
LP006 WELD_START
LP007 WELD_END
LP008 SCAN_START
LP009 SCAN_END
LP010 TAUSCHPUNKT
//ARGINFO
///ARGTYPE P,P,P,D,D,D,D,I
///COMMENT
ECKPUNKT_1
ECKPUNKT_2
ECKPUNKT_3
VERSATZ
KENNLINIE
V_WELD
USERFRAME
SPUR_SOLL
//INST
///DATE 2022/08/30 14:42
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,2,15,0,0,11,0,0
NOP
'--------------------------------
'ALTERNIERENDE STRICHRAUPEN?
SET ALTERNIEREN 1
'ARGUMENTE EINLESEN
GETARG Pkt_1 IARG#(1)
GETARG Pkt_2 IARG#(2)
GETARG Pkt_3 IARG#(3)
GETARG VERSATZ IARG#(4)
GETARG KENNLINIE IARG#(5)
GETARG V_WELD IARG#(6)
GETARG USERFRAME IARG#(7)
GETARG SPUR_SOLL IARG#(8)
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
' SCANZUGABE plus/minus setzen
SET SCANZUGABE 10000
'--------------------------------
' SCHWEISS TOOL auswaehlen
SET WELD_TOOL 0
'--------------------------------
' WINKEL FUER STECHEN SCHLEPPEN
SET STICHWINKEL 0
'--------------------------------
' DISTANZ FUER STECHEN SCHLEPPEN
SET STICHDISTANZ 0
'--------------------------------
'Inkrementalpunkte Berechnen
SET WELD_INKR Pkt_2
SET MOVE_INKR Pkt_1
SET OFFSET_INKR Pkt_3
SUB WELD_INKR Pkt_1
SUB MOVE_INKR Pkt_2
SUB OFFSET_INKR Pkt_1
GETE OFFSET_1 OFFSET_INKR (1)
GETE OFFSET_2 OFFSET_INKR (2)
GETE H_1 Pkt_1 (3)
GETE H_3 Pkt_3 (3)
IFTHENEXP OFFSET_1<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_1>0
	 SETE OFFSET_INKR (1) VERSATZ
ELSEIFEXP OFFSET_2<0
	 MUL VERSATZ -1
	 SETE OFFSET_INKR (2) VERSATZ
ELSEIFEXP OFFSET_2>0
	 SETE OFFSET_INKR (2) VERSATZ
ENDIF
SET Z_INKRE  EXPRESS ( H_3 - H_1 ) / ( SPUR_SOLL )
SETE OFFSET_INKR (3) Z_INKRE
CALL JOB:GET_ID ARGF"RAUPE_NR"
GETS SPUR_IST $RV
ADD SPUR_SOLL SPUR_IST
CALL JOB:SET_ID ARGFSPUR_IST ARGF"RAUPE_NR"
SET WELD_START Pkt_1
SET WELD_END Pkt_2
SET SCAN_START Pkt_1
SET SCAN_END Pkt_2
'ANSTELLWINKEL DER SCANPUNKTE UM
'X UND Y AUF 0 STELLEN
SETE SCAN_START (4) 0
SETE SCAN_START (5) 0
SETE SCAN_END (4) 0
SETE SCAN_END (5) 0
'--------------------------------
'SCHWEISSEN
CALL JOB:WELD_LINEAR ARGFWELD_START ARGFWELD_END ARGFV_WELD ARGFKENNLINIE ARGFWELD_TOOL ARGFV_MOVE ARGFSTICHWINKEL ARGFSTICHDISTANZ
'--------------------------------
'SCANNEN
CALL JOB:SCAN_LINEAR_ARG_P ARGFSCAN_END ARGFSCAN_START ARGFSCANZUGABE ARGFV_SCAN
'--------------------------------
'ZWISCHENRAUPENZEIT
TIMER T=1.00
'--------------------------------
'IDs setzen
CALL JOB:INC_NR ARGF"RAUPE_NR"
'--------------------------------
'SCHLEIFE
'--------------------------------
WHILEEXP SPUR_IST<SPUR_SOLL
	'--------------------------------
	'Punkte berechnen
	 ADD WELD_START OFFSET_INKR
	 ADD WELD_END OFFSET_INKR
	 ADD SCAN_START OFFSET_INKR
	 ADD SCAN_END OFFSET_INKR
	'--------------------------------
	'SCHWEI?RICHTUNG ALTERNIEREN
	'FALLS ERWÜESCHT
	 IFTHENEXP ALTERNIEREN=1
		 SET TAUSCHPUNKT WELD_START
		 SET WELD_START WELD_END
		 SET WELD_END TAUSCHPUNKT
		 SET TAUSCHPUNKT SCAN_START
		 SET SCAN_START SCAN_END
		 SET SCAN_END TAUSCHPUNKT
	 ENDIF
	'SCHWEISSEN
	 CALL JOB:WELD_LINEAR ARGFWELD_START ARGFWELD_END ARGFV_WELD ARGFKENNLINIE ARGFWELD_TOOL ARGFV_MOVE ARGFSTICHWINKEL ARGFSTICHDISTANZ
	'--------------------------------
	'Aufruf des Servicepositionsjobs
	'Roboter fährt aus akktueller
	'Position 150mm nach oben falls
	'Register 312 auf 1 gesetzt wird
	 CALL JOB:SERVICEPAUSE ARGFUSERFRAME ARGFWELD_TOOL
	'--------------------------------
	'SCANNEN
	 CALL JOB:SCAN_LINEAR_ARG_P ARGFSCAN_END ARGFSCAN_START ARGFSCANZUGABE ARGFV_SCAN
	'--------------------------------
	'ZWISCHENRAUPENZEIT
	 TIMER T=1.00
	 ADD SPUR_IST 1
	'--------------------------------
	'IDs setzen
	 CALL JOB:INC_NR ARGF"RAUPE_NR"
ENDWHILE
TCPOF
END
