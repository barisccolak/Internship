/JOB
//NAME HEFTEN
///FOLDERNAME PROBENVORBEREITUNG
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,0,10,0,0,3,0,0
LD000 V_MOVE
LD001 USERFRAME
LD002 V_WELD
LD003 TOOL
LD004 SPALTBREITE
LD005 KENNLINIE
LD006 VERSATZ
LD007 PENDEL_N_SOLL
LD008 PENDEL_N_IST
LD009 V_PENDEL
LP000 HEFTPUNKT
LP001 PKT_1
LP002 PKT_2
//INST
///DATE 2023/06/15 09:19
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,0,10,0,0,3,0,0
NOP
SET USERFRAME 4
SET TOOL 0
SET KENNLINIE 72
SET PENDEL_N_SOLL 1
SET SPALTBREITE 3000
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'WELD-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF30 ARGF"cm/min"
GETS V_WELD $RV
'WELD-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF120 ARGF"cm/min"
GETS V_PENDEL $RV
'Brenner in die mitte
'des Spaltes stellen!!!
'--------------------------------
'Basispunkt erzeugen
GETS HEFTPUNKT $PX000
CNVRT HEFTPUNKT HEFTPUNKT UF#(USERFRAME)
SET VERSATZ  EXPRESS SPALTBREITE / 2 + 500
'PKT_1
CALL JOB:SHIFT_PKT_ELEMENT ARGFHEFTPUNKT ARGF"y" ARGFVERSATZ
SET PKT_1 P100
MUL VERSATZ -1
'PKT_2
CALL JOB:SHIFT_PKT_ELEMENT ARGFHEFTPUNKT ARGF"y" ARGFVERSATZ
SET PKT_2 P100
'--------------------------------
'TCP Auswählen
SFTOF
TCPON TL#(TOOL)
MOVL PKT_1 V=V_MOVE
'SCHWEISSEN
SET PENDEL_N_IST 0
ARCON ASF#(KENNLINIE)
DOUT OT#(KENNLINIE) ON
TIMER T=0.30
MOVL PKT_2 V=V_WELD
TIMER T=0.30
WHILEEXP PENDEL_N_IST<PENDEL_N_SOLL
	 MOVL PKT_1 V=V_PENDEL
	 MOVL PKT_2 V=V_PENDEL
	 ADD PENDEL_N_IST 1
ENDWHILE
MOVL HEFTPUNKT V=V_PENDEL
ARCOF AEF#(KENNLINIE)
DOUT OT#(KENNLINIE) OFF
TCPOF
END
