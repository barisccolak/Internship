/JOB
//NAME WELD_MAEANDER_INKREMENTAL
///FOLDERNAME BLOCK_MAEANDER
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 0,2,14,0,0,6,0,0
LI000 SPUR_IST
LI001 WELD_TOOL
LD000 H_3
LD001 Z_INKRE
LD002 KENNLINIE
LD003 V_WELD
LD004 USERFRAME
LD005 V_MOVE
LD006 OFFSET_1
LD007 OFFSET_2
LD008 VERSATZ
LD009 H_1
LD010 SPUR_SOLL
LD011 SCANZUGABE
LD012 V_SCAN
LD013 V_WELD_OFFSET
LP000 PKT_1
LP001 PKT_2
LP002 PKT_3
LP003 WELD_MAIN_POS
LP004 WELD_MAIN_NEG
LP005 WELD_OFFSET
//ARGINFO
///ARGTYPE P,P,P,D,D,D,D,I
///COMMENT
ECKPUNKT_1
ECKPUNKT_2
ECKPUNKT_3
VERSATZ
KENNLINIE
V_WELD
USERFRAME
SPUR_SOLL
//INST
///DATE 2023/07/19 12:32
///ATTR SC,RW
///GROUP1 RB1
///LVARS 1,2,15,0,0,11,0,0
NOP
'ARGUMENTE EINLESEN
GETARG PKT_1 IARG#(1)
GETARG PKT_2 IARG#(2)
GETARG PKT_3 IARG#(3)
GETARG VERSATZ IARG#(4)
GETARG KENNLINIE IARG#(5)
GETARG V_WELD IARG#(6)
GETARG USERFRAME IARG#(7)
GETARG SPUR_SOLL IARG#(8)
'--------------------------------
SET V_WELD_OFFSET  EXPRESS V_WELD * 5
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'SCAN-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF10 ARGF"mm/s"
GETS V_SCAN $RV
'--------------------------------
' SCANZUGABE plus/minus setzen
SET SCANZUGABE 10000
'--------------------------------
' SCHWEISS TOOL auswaehlen
SET WELD_TOOL 0
'--------------------------------
'Inkrementalpunkte Berechnen
SET WELD_MAIN_NEG PKT_1
SET WELD_MAIN_POS PKT_2
SET WELD_OFFSET PKT_3
'--------------------------------
SUB WELD_MAIN_POS PKT_1
SUB WELD_MAIN_NEG PKT_2
SUB WELD_OFFSET PKT_1
'--------------------------------
GETE OFFSET_1 WELD_OFFSET (1)
GETE OFFSET_2 WELD_OFFSET (2)
IFTHENEXP OFFSET_1<0
	 MUL VERSATZ -1
	 SETE WELD_OFFSET (1) VERSATZ
ELSEIFEXP OFFSET_1>0
	 SETE WELD_OFFSET (1) VERSATZ
ELSEIFEXP OFFSET_2<0
	 MUL VERSATZ -1
	 SETE WELD_OFFSET (2) VERSATZ
ELSEIFEXP OFFSET_2>0
	 SETE WELD_OFFSET (2) VERSATZ
ENDIF
'--------------------------------
'Z_INKREMENT BERECHNEN UND ZU
'WELD_OFFSET HINZUFUEGEN
GETE H_1 PKT_1 (3)
GETE H_3 PKT_3 (3)
SET Z_INKRE  EXPRESS ( H_3 - H_1 ) / ( SPUR_SOLL )
SETE WELD_OFFSET (3) Z_INKRE
'TOOL TCP setzen
CALL JOB:SET_TCPON ARGFWELD_TOOL
TCPON TL#(WELD_TOOL)
'--------------------------------
MOVL PKT_1 V=V_MOVE
'--------------------------------
'Start TwinCAT and HDR record
CALL JOB:TRIGGER ARGF"UI_START"
TIMER T=0.10
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
TIMER T=1.00
CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
'IDs setzen
'CALL JOB:INC_NR ARGF "RAUPE_NR"
SET SPUR_IST 0
ARCON ASF#(KENNLINIE)
DOUT OT#(KENNLINIE) ON
'--------------------------------
WHILEEXP SPUR_IST<SPUR_SOLL
	 IFTHENEXP SPUR_IST>0
		 IMOV WELD_OFFSET V=V_WELD_OFFSET UF#(4)
	 ENDIF
	 IMOV WELD_MAIN_POS V=V_WELD UF#(4)
	 IMOV WELD_OFFSET V=V_WELD_OFFSET UF#(4)
	 IMOV WELD_MAIN_NEG V=V_WELD UF#(4)
	 ADD SPUR_IST 2
ENDWHILE
DOUT OT#(KENNLINIE) OFF
ARCOF AEF#(KENNLINIE)
TCPOF
'--------------------------------
'Stop TwinCAT and HDR record
CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
CALL JOB:TRIGGER ARGF"UI_STOP"
'Aufruf des Servicepositionsjobs
'Roboter fährt aus akktueller
'Position 150mm nach oben falls
'Register 312 auf 1 gesetzt wird
CALL JOB:SERVICEPAUSE ARGFUSERFRAME ARGFWELD_TOOL
'--------------------------------
END
