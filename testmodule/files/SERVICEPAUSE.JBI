/JOB
//NAME SERVICEPAUSE
///FOLDERNAME TWINCAT_KOMMUNIKATION
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,1,4,0,0,1,0,0
LI000 SERVICE
LD000 V_MOVE
LD001 USERFRAME
LD002 WELD_TOOL
LD003 Z_VERSATZ
LP000 IST_POS
//ARGINFO
///ARGTYPE D,D,,,,,,
///COMMENT
USERFRAME
WERKZEUG






//INST
///DATE 2022/09/08 11:54
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,1,4,0,0,1,0,0
NOP
GETREG SERVICE MREG#(312) LOW
RET IF SERVICE=0
'ARGUMENTE EINLESEN - Translation
'--------------------------------
GETARG USERFRAME IARG#(1)
GETARG WELD_TOOL IARG#(2)
'--------------------------------
SET Z_VERSATZ 250000
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF50 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'TCP des richtigen Werkzeuges
'auswählen
TCPON TL#(WELD_TOOL)
'--------------------------------
'Basispunkt erzeugen
GETS IST_POS $PX000
CNVRT IST_POS IST_POS UF#(USERFRAME)
'Punkt um Z_VERSATZ mm anheben
CALL JOB:SHIFT_PKT_ELEMENT ARGFIST_POS ARGF"z" ARGFZ_VERSATZ
SET IST_POS P100
'--------------------------------
'SERVICEPOSITION anfahren
MOVL IST_POS V=V_MOVE
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
'--------------------------------
'Hält Position solange bis
'Register 312 auf Null geht
WHILEEXP SERVICE=1
	 TIMER T=1.00
	 GETREG SERVICE MREG#(312) LOW
ENDWHILE
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
'Punkt um 150 mm absenken
MUL Z_VERSATZ -1
CALL JOB:SHIFT_PKT_ELEMENT ARGFIST_POS ARGF"z" ARGFZ_VERSATZ
SET IST_POS P100
'AUSGANGSPOSITION anfahren
'MOVL IST_POS V=V_MOVE
TCPOF
END
