/JOB
//NAME LLT
///FOLDERNAME TRIGGER_JOBS
//POS
///NPOS 0,0,0,0,0,0
//ALIAS
///LVARS 1,3,0,0,1,0,0,0
LB000 B_IST
LI000 SENSOR
LI001 I_IST
LI002 I_SOLL
LS000 BEFEHL
//ARGINFO
///ARGTYPE I,S,,,,,,
///COMMENT
SENSOR
BEFEHL






//INST
///DATE 2021/03/16 10:51
///ATTR SC,RW
///LVARS 2,3,0,0,2,0,0,0
NOP
'ARGUMENTE EINLESEN
GETARG SENSOR IARG#(1)
GETARG BEFEHL IARG#(2)
'Status lokal auslesen
IFTHENEXP SENSOR=1
	 GETREG I_IST MREG#(63) LOW
ELSEIFEXP SENSOR=2
	 GETREG I_IST MREG#(63) HIGH
ELSE
	 MSG "FALSCHE EINGABE LLT"
	 SET B000  EXPRESS -500000
ENDIF
'Status in BYTE konvertieren
SET B_IST I_IST
'Befehls-Bit setzen
IFTHENEXP BEFEHL="LASER_EIN"
	 OR B_IST 1
ELSEIFEXP BEFEHL="LASER_AUS"
	 AND B_IST 254
ELSEIFEXP BEFEHL="TRIG_EIN"
	 OR B_IST 2
ELSEIFEXP BEFEHL="TRIG_AUS"
	 AND B_IST 253
ELSEIFEXP BEFEHL="TRIG_EXT"
	 OR B_IST 4
ELSEIFEXP BEFEHL="TRIG_INT"
	 AND B_IST 251
ELSE
	 MSG "FALSCHE EINGABE LLT"
	 SET B000  EXPRESS -500000
ENDIF
'Status in INTEGER konvertieren
SET I_SOLL B_IST
'Status in Register schreiben
IFTHENEXP SENSOR=1
	 SETREG MREG#(63) LOW I_SOLL
ELSEIFEXP SENSOR=2
	 SETREG MREG#(63) HIGH I_SOLL
ENDIF
END
