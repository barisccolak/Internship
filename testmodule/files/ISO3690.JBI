/JOB
//NAME ISO3690
///FOLDERNAME WELD_JOBS
//POS
///NPOS 0,0,0,1,0,0
///USER 4
///TOOL 1
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
P00100=-51.450,-41.710,12.601,0.0000,0.0000,0.0000
//ALIAS
///LVARS 0,0,7,0,0,3,0,0
LD000 V_WELD
LD001 KENNLINIE
LD002 TCP_NR
LD003 V_MOVE
LD004 USERFRAME
LD005 X_OFFSET
LD008 Z_ROTATION
LP000 WELD_START
LP001 WELD_END
LP002 REST
//INST
///DATE 2023/02/10 11:01
///COMM Basic Linear Weld Job
///ATTR SC,RW
///GROUP1 RB1
///LVARS 0,0,10,0,0,6,0,0
NOP
CALL JOB:TRIGGER_RESET
CALL JOB:TRIGGER ARGF"PROGRAMM_EIN"
SET TCP_NR 0
SET KENNLINIE 7
SET USERFRAME 14
SET Z_ROTATION 900000
SET X_OFFSET 87000
CALL JOB:SET_TCPON ARGFTCP_NR
TCPON TL#(TCP_NR)
'--------------------------------
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET ARGF40000 ARGF-10000
SET WELD_START P100
SETE WELD_START (6) Z_ROTATION
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET ARGF130000 ARGF-10000
SET WELD_END P100
SETE WELD_END (6) Z_ROTATION
CALL JOB:PUNKTE_ERZEUGEN_UF ARGFUSERFRAME ARGFX_OFFSET ARGF400000 ARGF200000
SET REST P100
SETE REST (6) Z_ROTATION
'--------------------------------
'WELD-Geschwindigkeit
CALL JOB:GET_V ARGF31 ARGF"cm/min"
GETS V_WELD $RV
'--------------------------------
'MOVE-Geschwindigkeit
'       [mm/s]
CALL JOB:GET_V ARGF400 ARGF"mm/s"
GETS V_MOVE $RV
'--------------------------------
'Zu WELD_START fahren
MOVL WELD_START V=V_MOVE
'--------------------------------
'Start TwinCAT and HDR record
CALL JOB:TRIGGER ARGF"UI_START"
TIMER T=0.10
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_EIN"
TIMER T=1.00
CALL JOB:TRIGGER ARGF"SCHWEISSEN_EIN"
CALL JOB:XIRIS ARGF1 ARGF"TRIG_EIN"
'--------------------------------
'SCHWEISSEN
DOUT OT#(KENNLINIE) ON
ARCON ASF#(KENNLINIE)
MOVL WELD_END V=V_WELD
ARCOF AEF#(KENNLINIE)
DOUT OT#(KENNLINIE) OFF
'--------------------------------
'Stop TwinCAT and HDR record
CALL JOB:XIRIS ARGF1 ARGF"TRIG_AUS"
CALL JOB:TRIGGER ARGF"SCHWEISSEN_AUS"
TIMER T=1.00
CALL JOB:XIRIS ARGF1 ARGF"VIDEO_AUS"
CALL JOB:TRIGGER ARGF"UI_STOP"
'--------------------------------
MOVL REST V=V_MOVE
'ENDE
TCPOF
CALL JOB:TRIGGER ARGF"PROGRAMM_AUS"
CALL JOB:TRIGGER_RESET
END
